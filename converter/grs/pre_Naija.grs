% Some specificities used in Spoken annotation (Naija et French Spoken)
package scrap_spec {
% Remove `scrap` deep extension

  rule scrap_det {
    pattern { e: N -[1<>root, deep=scrap]-> M; M [upos=DET] }
    commands {
      del_feat e.deep;
      M.ScrapPos = M.upos;
      M.upos=PRON;
    }
  }

  rule scrap_sconj {
    pattern { e: N -[1<>root, deep=scrap]-> M; M [upos=SCONJ] }
    commands {
      del_feat e.deep;
      M.ScrapPos = M.upos;
      M.upos=VERB;
    }
  }
}


% Macrosyntax is annotated with PUNCT in SUD but it gives invalid UD structures (non projectivity).
% Change Macrosyntax annotation with upos `X` and relation `unk`
package PUNCT_to_X {
  rule punct_pos {
    pattern { N [upos=PUNCT] }
    commands { N.upos=X }
  }

  rule punct_rel {
    pattern { e: N -[punct]-> M; }
    commands { e.1 = unk }
  }
}

% A few token have POS in SUD whiwh are not compatible with validation constraints.
package change_POS {
  rule na {
    pattern { X[lemma=na, upos = PART] }
    commands  { X.upos=AUX }
  }

  rule its {
    pattern { X[lemma="it's"|"dat's", upos = PART] }
    commands  { X.upos=VERB }
  }

  rule be {
    pattern { X[lemma=be, upos = VERB] ; X -[1=comp,2=pred]-> Y }
    commands  { X.upos=AUX }
  }
}

% Some specificities used in Spoken annotation (Naija et French Spoken)
package spoken {
% Remove `scrap` deep extension
  rule scrap {
    pattern { e: N -[deep=scrap]-> M; }
    commands { del_feat e.deep }
  }

  rule conjcoord {
    pattern { e: N -[conj:coord]-> M; }
    commands { del_edge e; add_edge N -[conj]-> M; }
  }

  rule conjappos {
    pattern { e: N -[conj:appos]-> M; }
    commands { del_edge e; add_edge N -[appos]-> M; }
  }
}


package Naija_MISC {
  rule comp_pred_agent {
    pattern { N -[comp:pred]-> N1; e: N -[comp:pred@agent]-> N2; }
    commands { del_edge e;  add_edge N -[comp:obl@agent]-> N2; }
  }

  rule cc {
    pattern { X -[cc]-> Y ; e : Y -[punct|discourse|parataxis:parenth]-> Z }
    commands {
      add_edge f : X -> Z; f.label = e.label;
      del_edge  e;
    }
  }
}


strat main {
  Seq (
    Onf (scrap_spec),
    Onf (PUNCT_to_X),
    Onf (change_POS),
    Onf (spoken),
    Onf (Naija_MISC),
  )
}