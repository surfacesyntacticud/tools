% The "irregular_expr.grs" file includes packages applying a specific treatment to idioms, titles and converted words. What they have in common is the fact that their syntactic behaviour towards the outside world is not regular.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The package [add_edges] turns all features InIdiom (resp. InTitle) into a edge IDIOM (resp. TITLE) from the head to internal nodes of irregular expressions.
package add_edges {
  rule init_idiom {
    pattern { H [PhraseType=Idiom]; H-> N; N[InIdiom=Yes] }
    commands { del_feat N.InIdiom; add_edge H -[IDIOM]-> N }
  }

  rule iter_idiom {
    pattern { H -[IDIOM]-> M; M -> N; N[InIdiom=Yes] }
    commands { del_feat N.InIdiom; add_edge H -[IDIOM]-> N }
  }

  rule init_title {
    pattern { H [PhraseType=Title]; H-> N; N[InTitle=Yes] }
    commands { del_feat N.InTitle; add_edge H -[TITLE]-> N }
  }

  rule iter_title {
    pattern { H -[TITLE]-> M; M -> N; N[InTitle=Yes] }
    commands { del_feat N.InTitle; add_edge H -[TITLE]-> N }
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The package [add_nodes] insert a new node for each Idiom/Title/Conv expression.
% The new node is linked to the head with the HEAD relation and to other elements by a IDIOM/TITLE relation.
package add_nodes {
  rule idiom {
    pattern{ H [ExtPos,PhraseType=Idiom] }
    without{H[upos=AUX|VERB, VerbForm, ExtPos=VERB]}
    without{H[ExtPos=VERB]; H -[IDIOM]-> AUX; AUX[upos=AUX,VerbForm]}
    commands{
      add_node I :< H;
      add_edge I -[IDIOM]-> H; add_edge I -[HEAD]-> H;
      shift_in H =[^IDIOM|TITLE]=> I;
      shift_out H =[IDIOM]=> I;
      I.upos = H.ExtPos; del_feat H.ExtPos;
      I.Type = H.PhraseType; del_feat H.PhraseType;
    }
  }

  rule idiom_verb {
    pattern{ H [upos=AUX|VERB, VerbForm,ExtPos=VERB,PhraseType=Idiom] }
    without{H -[IDIOM]-> AUX; AUX[upos=AUX,VerbForm]}
    commands{
      add_node I :< H;
      add_edge I -[IDIOM]-> H; add_edge I -[HEAD]-> H;
      shift_in H =[^IDIOM|TITLE]=> I;
      shift_out H =[IDIOM]=> I;
      I.upos = H.ExtPos; del_feat H.ExtPos;
      I.Type = H.PhraseType; del_feat H.PhraseType;
      I.VerbForm = H.VerbForm
    }
  }

  rule idiom_cop {
    pattern{
      H [ExtPos=VERB,PhraseType=Idiom];
      e:H -[IDIOM]-> AUX; AUX[upos=AUX,VerbForm]}
    commands{
      add_node I :< H;
      add_edge I -[IDIOM]-> H; add_edge I -[HEAD]-> H;
      shift_in H =[^IDIOM|TITLE]=> I;
      shift_out H =[IDIOM]=> I;
      del_edge e; add_edge I -[IDIOM]-> AUX;
      I.upos = H.ExtPos; del_feat H.ExtPos;
      I.Type = H.PhraseType; del_feat H.PhraseType;
      I.VerbForm = AUX.VerbForm
    }
  }

  rule title {
    pattern{ H [ExtPos,PhraseType=Title] }
    without{ * -[expr]-> H }
    commands{
      add_node T :< H; add_edge T -[TITLE]-> H; add_edge T -[HEAD]-> H;
      shift_in H =[^IDIOM|TITLE]=> T;
      shift_out H =[TITLE]=> T;
      T.upos = H.ExtPos; del_feat H.ExtPos;
      T.Type = H.PhraseType; del_feat H.PhraseType;
    }
  }

  rule conv {
    pattern{ H [ExtPos,!PhraseType] }
    without{ * -[HEAD]-> H }
    commands{
      add_node C :< H; add_edge C -[CONV]-> H; add_edge C -[HEAD]-> H;
      shift H =[^IDIOM|TITLE]=> C;
      C.upos = H.ExtPos; del_feat H.ExtPos;
      C.lemma = H.lemma
    }
  }

  % When an idiom is included in a title, the new node must receive a TITLE relation
  rule mwe_in_title {
    pattern { E -[HEAD]-> N; T -[TITLE]-> N; }
    without { T -[TITLE]-> E; }
    commands { add_edge T -[TITLE]-> E; }
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The package [ext_dep] move dependencies external to the idiom, title, conv expresion, on the new node.
package ext_dep {
  rule idiom {
    pattern { H -[IDIOM]-> N; e: N -> M}
    without { H -[IDIOM]-> M; }
    without{ * -[IDIOM]-> M}
    commands {
      add_edge f:H->M; f.label = e.label;
      del_edge e;
    }
  }

  rule title {
    pattern { H -[TITLE]-> N; e: N -> M;}
    without { H -[TITLE]-> M; }
     without{ * -[TITLE]-> M}
   commands {
     add_edge f:H->M; f.label = e.label;
     del_edge e;
   }
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% sequentialize the 3 previous packages into one strategy.
strat specific_expr_init {
  Seq (
    Onf(add_edges),
    Onf(add_nodes),
    Onf(ext_dep),
  )
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% replace edges IDIOM, TITLE by features
package del_edges {
  rule idiom_head {
    pattern { I[Type]; I -[HEAD]-> H; e:I -[IDIOM]-> H; }
    commands {
      del_edge e;
      H.ExtPos = I.upos;
      H.PhraseType = I.Type;
    }
  }

  rule idiom {
    pattern { e:I -[IDIOM]-> H }
    without { I -[HEAD]-> H }
    commands {
      del_edge e;
      H.InIdiom = Yes;
    }
  }

  rule title_head {
    pattern { T -[HEAD]-> H; e:T -[TITLE]-> H }
    commands {
      del_edge e;
      H.ExtPos = T.upos;
      H.PhraseType = T.Type;
    }
  }

  rule title {
    pattern { e:T -[TITLE]-> H }
    without { T -[HEAD]-> H }
    without{T -[HEAD]-> H1; H1 -[HEAD]-> H; H1[Type=Idiom]}
    commands {
      del_edge e;
      H.InTitle = Yes;
    }
  }

%The title head is also an idiom head
  rule title_idiom {
    pattern { e:T -[TITLE]-> H;T -[HEAD]-> H1; H1 -[HEAD]-> H; H[Type=Idiom]  }
    without { T -[HEAD]-> H }
    commands {
      del_edge e;
    }
  }
  rule conv_head {
    pattern { C -[HEAD]-> H; e:C -[CONV]-> H }
    commands {
      del_edge e;
      H.ExtPos = C.upos;
    }
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% remove nodes
package del_nodes {
  rule del_specif_expr{
    pattern{ N[!Shared];e: N -[HEAD]-> H }
    commands{del_edge e; shift N ==> H;del_node N}
  }

  rule del_shared_specif_expr_{
    pattern{ N[Shared=Yes]; e: N -[HEAD]-> H }
    commands{del_edge e; shift N ==> H;del_node N; H.Shared=Yes}
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% sequentialize the 3 previous packages into one strategy.
strat specific_expr_close {
  Seq (
    Onf(del_edges),
    Onf(del_nodes),
  )
}
