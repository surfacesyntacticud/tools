%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The "irregular_expr.grs" file includes packages applying a specific treatment to idioms, titles and translated. What they have in common is the fact that their syntactic behaviour towards the outside world is not regular.
include "irregular_expr.grs"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Remove all information specific to Enhanced UD.
package eud_to_ud {
  rule deep { % remove add enhanced relations
    pattern { e:N -[re"E:.*"]-> M }
    commands { del_edge e}
  }

  rule empty { % remove empty nodes
    pattern { N [_UD_empty=Yes] }
    commands { del_node N }
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD sub-relations are renamed as SUD sub-relations or relations.
% The rules are arranged in the alphabetic order of the SUD resulting relations or sub-relations.
package sub_relations {
% comp
%=========
% Sub-relations DEP:COMP are renamed COMP in SUD.
  rule comp_dep {
    pattern { e:H -[dep:comp]-> D; }
    commands {del_edge e; add_edge H -[comp]-> D}
  }
  
% All sub-relations of COMP, for which the extension does not change from UD to SUD.
  rule comp {
    pattern { e:H -[advcl:cleft|ccomp:obj|ccomp:obl]-> D; }
    commands {e.1=comp}
  }

% UD XCOMP direct object or oblique argument.
  rule comp-obj-obl_xcomp {
    pattern { e:H -[xcomp:obj|xcomp:obl]-> D; }
    commands {e.1=comp;e.deep=x}
  }
  

% Clausal argument.
  rule comp-obj-obl-pred_ccomp {
    pattern { e:H -[1=ccomp,2=obj|obl|pred]-> D; }
    commands {e.1=comp}
  }
  
% In the impersonal diathesis, the deep subject, which is a NSUBJ or CSUBJ dependent in UD, becomes a direct object in SUD with the deep extension AGENT.
% The impersonal diathesis is detected by means of the impersonal subject.
% The UD relation that is renamed as a SUD relation is not a sub-relation but it must be processed before the general processing of relations.
% Ex: Il arrive deux personnes.  - Il se dit que tu viendras. - Il m'arrive de faire des erreurs. - Il s'est vendu une grande maison aujourd'hui. - Il a pu se vendre une grande maison aujourd'hui. 
  rule comp-obj-agent_subj {
    pattern{e:H -[nsubj|csubj]-> D; H -[expl:subj]-> *}
    commands{del_edge e; add_edge H -[comp:obj@agent]-> D }
  }

% In the impersonal passive diathesis, the deep subject, which is a NSUBJ:PASS or CSUBJ:PASS dependent in UD, becomes an OBJ:PASS dependent in SUD.
% The impersonal diathesis is detected by means of the impersonal subject,which may not be a direct dependent of the main verb, hence the need for several rules.
% Il sera vendu une grande maison aujourd'hui - Il est prévu de continuer. - Il a été vendu une grande maison aujourd'hui. - Il a été prévu de continuer. - Il peut avoir été vendu une grande maison aujourd'hui.
  rule comp-obj-pass_subj {
    pattern{e:H -[1=csubj, 2=pass]-> D; H -[expl:subj]-> *}
    commands{del_edge e; add_edge H -[comp:obj@pass]-> D; }
  }
  
% Oblique argument.
  rule comp_obl {
    pattern { e:H -[1=obl, 2=arg|comp]-> D; } % comp pour Spoken
    commands {e.1=comp; e.2=obl}
  }
  
% Predicative non clausal argument.
  rule comp-pred_xcomp {
    pattern { e:H -[xcomp:pred]-> D}
    without{D[upos=AUX|VERB] }
    commands {e.1=comp;e.2=pred}
  }

% Predicative clausal argument.
  rule comp-pred_ccomp {
    pattern { e:H -[ccomp:pred]-> D; }
    commands {e.1=comp; e.2=pred}
  }
  
% Predicative clausal argument.
  rule comp-pred-x_xcomp {
    pattern { e:H -[xcomp:pred]-> D;D[upos=AUX|VERB] }
    commands {del_edge e; add_edge H -[comp:pred@x]-> D}
  }
    
 % Oblique modifier.
  rule mod {
    pattern { e:H -[obl:mod]-> D; }
    commands {e.1=mod; del_feat e.2}
  }

% In an impersonal construction, the impersonal subject, which is an EXPL dependent in UD, becomes an  explicit subject in SUD with the deep extension expl.
% Ex: Il arrive deux personnes. - Il faut un bilan. - Il s'agit d'une erreur. - Il a été vendu une grande maison. - il se dit que tu viendras.
  rule subj_expl {
    pattern { e:H -[expl:subj]-> D; }
    without{H -[csubj|nsubj|csubj:pass|nsubj:pass]-> *}
    commands {e.1=subj;e.deep=expl; del_feat e.2}
  }

  rule other_expl {
    pattern { e:H -[1=expl, 2<>subj|comp|pass]-> D; }
    commands {del_edge e; add_edge H -[unk@expl]-> D}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD extensions of relations are transformed into deep extensions of SUD relations and deep extensions "reversed" are deleted.
% Examples of cop extensions: cop:locat in UD_Polish-LFG and cop:own in UD_Finnish-* and UD_Karelian-KKPP
package rel_extensions {
% When a relation with an extension is different from COMP, its extension is transformed into a deep extension.
  rule change_ext{
    pattern{e: H -[1=*,2=*]-> D}
    without{ H -[1=aux|comp|subj]-> D}
    without{ H -[conj:emb]-> D}
    commands{ add_edge H -[1=e.1,deep=e.2]-> D; del_edge e}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD relations are renamed as SUD relations or sub-relations.
% Rem: the order of the rules follows the alphabetic order of the names of the resulting relations.
package relations {


%comp:obj
%=========
  rule comp_obj {
    pattern { e:H -[1=obj]-> D; }
    commands { e.1=comp;e.2=obj }
  }

% We make the assumption that a clausal complement that is not introduced with an adposition is a direct object.
  rule comp_obj_ccomp {
    pattern {e:H -[1=ccomp]-> D; H[upos <> ADJ|ADV|NOUN];}
    without{H -[obj|comp:obj]-> *}
    without{ D -[mark]-> PREP; PREP[upos=ADP]}
    without{H -[aux@pass|comp:aux@pass]-> *}
    commands{e.1=comp;e.2=obj}
  }

  rule comp_obj_x {
    pattern { e:H -[1=xcomp]-> D;D[upos=AUX|VERB,VerbForm=Inf] }
    without{H -[comp:aux@pass|aux@pass]-> *}
    commands {e.1=comp;e.2=obj;e.deep=x}
  }

% comp:obl
%=========
  rule comp_obl_ccomp {
    pattern { e:H -[1=ccomp]-> D; D -[mark]-> PREP; PREP[upos=ADP]}
    commands {e.1=comp;e.2=obl}
  }

  rule comp_obl_ccomp_non-verb {
    pattern { e:H -[1=ccomp]-> D; H[upos = ADJ|ADV|NOUN]; }
    commands {e.1=comp;e.2=obl}
  }
  
  rule comp_obl_ccomp-dir {
    pattern {
      e:H -[1=ccomp]-> D;
      H -[1=comp,2=obj]-> D1
    }
    commands {e.1=comp;e.2=obl}
  }

  rule comp_obl_ccomp_pass {
    pattern {
      e:H -[1=ccomp]-> D; 
      H1 -[comp:aux@pass]-> H
    }
    commands {e.1=comp;e.2=obl}
  }

  rule comp_obl {
    pattern { e:H -[1=iobj]-> D; }
    commands {e.1=comp;e.2=obl}
  }

  rule comp_obl_x {
    pattern { e:H -[1=xcomp]-> D; D[upos=AUX|VERB]; D -[mark]-> *}
    commands {e.1=comp;e.2=obl;e.deep=x}
  }
  
  rule comp_obl-dir1_x {
    pattern {
      e:H -[1=xcomp]-> D;D[upos=AUX|VERB,VerbForm=Inf];
       H -[1=comp,2=obj]-> D1}
    without{D -[mark]-> *}
    commands {e.1=comp;e.2=obl;e.deep=x}
  }

  rule comp_obl-dir2_x {
    pattern {
      e:H -[1=xcomp]-> D;D[upos=AUX|VERB,VerbForm=Inf];
       AUX -[comp:aux@pass]-> H}
    without{D -[mark]-> *}   
    commands {e.1=comp;e.2=obl;e.deep=x}
  }

  rule comp_obl_agent {
    pattern { e:H -[1=obl,deep=agent]-> D }
    commands {e.1=comp;e.2=obl;e.deep=agent}
  }

% comp:pred
%==========
% A direct argument with the control of its subject is considered  as a predicative complement in SUD.
  rule comp_pred {
    pattern { e:H -[1=xcomp]-> D}
    without{D[upos=AUX|VERB] }
    commands {e.1=comp;e.2=pred}
  }
  
  rule comp_pred_part {
    pattern { e:H -[1=xcomp]-> D; D[upos=AUX|VERB,VerbForm=Part]}
    commands {add_edge H -[comp:pred@x]-> D; del_edge e}
  }


% mod
%====
% This rule is too strong for nummod, because in many languages, the relation is DET when D precedes H.
  rule mod {
    pattern { e:H -[1=advcl|advmod|amod|nummod]-> D; }
    commands {e.1=mod}
  }

% An ACL or NMOD complement of a noun that is not introduced with an adposition or a conjunction is a modifier.
  rule mod_noun {
    pattern { e:H -[1=acl|nmod]-> D}
    without{ D-[case]-> PREP; PREP[upos=ADP]}
    commands {e.1=mod}
  }


% subj
%=====
  rule subj {
    pattern { e:H -[1=nsubj|csubj]-> D}
    commands {e.1=subj}
  }

% udep
%=====
% An ACL or NMOD complement of a noun that is introduced with an adposition or a conjunction is a modifier or an argument.
  rule udep {
    pattern { e:H -[1=acl|nmod]-> D; D -[case]-> PREP; PREP[upos=ADP|SCONJ]}
    commands {e.1=udep}
  }

% TODO: replace next to rules by "without { e.deep=agent }"
  rule udep_obl1 {
    pattern { e:H -[1=obl, !deep]-> D}
    commands {e.1=udep}
  }
  
  rule udep_obl2 {
    pattern { e:H -[1=obl, deep<>agent|lvc]-> D}
    commands {e.1=udep}
  }
    
  rule udep_obl-lvc {
    pattern { e:H -[1=obl, deep=lvc]-> D}
    commands {e.1=comp;e.2=obl}
  }

% unk
%=====
  rule unk {
    pattern { e:H -[1=dep]-> D; }
    commands {e.1=unk}
  }

  rule unk_expl {
    pattern { e:H -[expl]-> D; }
    commands {
      del_edge e;
      add_edge H -[unk@expl]-> D;
    }
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% All aux, case, cop and mark relations are reversed.
% The reverse procedure consists of 4 step
% 1) package [normalize_caus_pass]: change aux:caus into caus relations and aux:pass into pass relations (doing this we have all relations to be reversed at the same top level);
% 2) package [reverse_edges]: reverse edges, add the edge feature deep=REV. shift_in are done and shift_out is done only on the subsets of reversable relations (case, cop, caus, pass, aux, mark); other shift_out operations are postponed to another package;
% 3) package [shift_out_edges]: generic rules for selection of relations that should move from the old head onto the new head;
% 4) rule [remove_rev]: remove the @REV extension;
% 5) package [restore_caus_pass]: symetric to step 1, change caus into aux:caus and pass into aux:pass
package reverse_relations{
  package normalize_caus_pass {
    rule caus {
      pattern { e:M -[aux:caus]-> N }
      commands { del_edge e; add_edge M -[caus]-> N }
    }

    rule pass {
      pattern { e:M -[aux:pass]-> N }
      commands { del_edge e; add_edge M -[pass]-> N }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
% When several dependents compete for replacing the same head, the rule of starting with the closest dependent does not work in the cases of extraposition of the predicate.
% Ex: fr-ud-test_00067-  ... ce que nous avons été ...
% Ex: fr-ud-train_00086- ... quelle que soit la nature de ses pratiques, ...
% The rule that works in all cases is based on an order between types of relations that must be reversed.
%The order chosen below is the following:
% (1) case < aux, caus < aux,  cop < aux, pass < aux;
% (2) case < caus, cop < caus;
% (3) case < cop ;
% (4) aux < mark, case < mark, caus < mark, cop < mark, pass < mark;
% (5) aux <> aux, case <> case, caus <> caus, cop <> cop, mark <> mark, pass <> pass; the order is determined by the order of proximity with respect the head
  package reverse_edges {
    rule aux {
      pattern { e:H -[1=aux, !deep]-> AUX }
      without { H -[1=case|cop|caus|pass, !deep]-> AUX1 } % rule (1) above
      without { H -[1=aux, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=aux, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|mark,!deep]=> AUX;
      }
    }

    rule case {
      pattern { e:H -[1=case, !deep]-> AUX }
      without { H -[1=case, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=case, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|case|caus|cop|mark|pass,!deep]=> AUX;
      }
    }

    rule caus {
      pattern { e:H -[1=caus, !deep]-> AUX }
      without { H -[1=case|cop, !deep]-> AUX1 } % rule (2) above
      without { H -[1=caus, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=caus, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|mark|pass,!deep]=> AUX;
      }
    }

    rule cop {
      pattern { e:H -[1=cop, !deep]-> AUX }
      without { H -[1=case, !deep]-> AUX1 } % rule (3) above
      without { H -[1=cop, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=cop, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|cop|mark|pass,!deep]=> AUX;
      }
    }

    rule mark {
      pattern { e:H -[1=mark, !deep]-> AUX }
      without { H -[1=aux|case|caus|cop|pass, !deep]-> AUX1 } % rule (4) above
      without { H -[1=mark, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=mark, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=mark,!deep]=> AUX;
      }
    }

    rule pass {
      pattern { e:H -[1=pass, !deep]-> AUX }
     % without { H -[1=case|cop|caus, !deep]-> AUX1 }
      without { H -[1=pass, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=pass, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge e: AUX -> H;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|case|cop|mark|pass,!deep]=> AUX;
      }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
% The source of some relations must rise from the old head D to the new head H.
package shift_out_edges{
% All relations that rise without any restriction.
  rule all{
    pattern {H -[deep=REV]-> D;e: D -[cc|discourse|vocative]-> D1}
    without{ D -[cc]-> D1; H << D1} %Ex:  fr-ud-train_12306 - ... sont soit des fanatiques, soit des hypocrites,
    commands{add_edge e: H -> D1;del_edge e}}

% Agent relations in causative and impersonal constructions.
  rule agent {
    pattern{H -[1=caus|cop,deep=REV]-> D; e: D -[deep=agent]-> D1}
    commands{add_edge e: H -> D1;del_edge e}
  }

% All relations that rise inside a clause.
  rule all_mark_embedded{
    pattern {
      H -[1<>mark,deep=REV]-> D;
      e: D -[comp:cleft|dislocated|parataxis|vocative]-> D1;}
    without{H1 -[1 <> cop]-> H; H -[1=case]-> D; D -[parataxis]-> D1}
    %without{H -[1=aux|caus|pass|cop]->D; D -[csubj:impers|nsubj:impers|expl:comp]-> D1}
    commands{add_edge e: H -> D1;del_edge e}
  }

  rule subj_mark_embedded{
    pattern {
      H -[1<>mark,deep=REV]-> D;
      e: D -[1=subj]-> D1}
    commands{add_edge e: H -> D1;del_edge e}
  }
  rule mod_clause_mark_embedded{
    pattern {
      H -[1<>mark,deep=REV]-> D;
      e: D -[1=mod]-> D1;D1[upos=SCONJ]
    }
    commands{add_edge e: H -> D1;del_edge e}
  }
  
% Relations with their source D after the head of a subordinated clause and their dependent D1 before.
  rule all_mark_outside_left{
    pattern {
      H -[1=mark,deep=REV]-> D;
      e: D -[1=dislocated|subj|vocative]-> D1;
      D1 << H; H << D}
    commands{add_edge e: H -> D1;del_edge e}
  }

% Relations with their source D before the head of a subordinated clause and their dependent D1 afterwards.
  rule all_mark_outside_right{
    pattern {
      H -[1=mark,deep=REV]-> D;
      e: D -[1=dislocated|subj]-> D1;
      D1 >> H; H >> D}
    commands{add_edge e: H -> D1;del_edge e}
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is on the right of D.
  rule H_D_D1{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=mod|udep|punct]-> D1; D << D1}
    without{D -[mod@relcl]-> D1}
    without{D[upos=NOUN|NUM|PRON|PROPN|X]; D -[mod|udep]-> D1; D1[upos=ADJ|NOUN|NUM|PRON|PROPN|X]}
    without{H -[1=case|mark]-> D;  D -[mod|udep]-> D1; D1[upos=ADP]}
    without{D -[1=comp]-> D1}
    without{D -> D2; D1 << D2} 
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = D2.lemma}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = ")"; D2.lemma = "("}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = "»"; D2.lemma = "«"}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = "]"; D2.lemma = "["}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is between D and H.
  rule H_D1_D{
    pattern{
      H -[1=aux|caus|cop|pass,deep=REV]-> D;
      e: D -[mod]-> D1;
      H << D1; D1 << D}
    without{ H -[1=case]-> D; D -[mod]-> D1 }
    without{ D -> D2; H << D2; D2 << D1}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Relations that rise when their source D is on the left of H and when their dependent D1 is on the right of H.
  rule D_H_D1{
    pattern{H -[deep=REV]-> D;  H >> D; e: D -[1=mod|punct]-> D1; D1 >> H}
    without{D -[comp:obl]-> D1}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Relations that rise when their source D is on the left of D1 and when their dependent D1 is also on the left of H.
  rule D_D1_D{
    pattern{H -[1=cop,deep=REV]-> D; D << D1; e: D -[1=mod]-> D1; D1 << H}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is on the left of H.
  rule D1_H_D{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=mod|orphan|punct|reparandum|udep]-> D1; D1 << H}
    without{D -[obl:agent|obl:arg]-> D1}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Relations that rise when their source D is on the left of H and when their dependent D1 is on the left of D.
  rule D1_D_H{
    pattern{
      H -[deep=REV]-> D;  H >> D;
      e: D -[1=mod|punct]-> D1; D >> D1
    }
    commands{add_edge e: H -> D1; del_edge e}
  }

% Conj relations in the general case.
  rule conj{
    pattern{
      H -[deep=REV]-> D; % H << D;
      e: D -[1=conj]-> D1;
      D.upos <> D1.upos;
    }
    without{D[upos=NOUN|PRON|PROPN|NUM|X]; D1[upos=NOUN|PRON|PROPN|NUM|X]}
    without{D[upos=ADJ]; D1[upos=VERB,VerbForm=Part]}
    without{D1[upos=ADJ]; D[upos=VERB,VerbForm=Part]}
    without{D[upos=ADP|ADV]; D1[upos=ADP|ADV]}
    without{D[upos=AUX|VERB]; D1[upos=AUX|VERB]; D1.VerbForm=D.VerbForm}
    commands{add_edge e: H -> D1; del_edge e}
  }

% Conj relations when the conjunct heads are verbs.
  rule conj_verb{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=conj]-> D1;
      D[upos=AUX|VERB,VerbForm]; D1[upos=AUX|VERB, VerbForm];
       D.VerbForm <> D1.VerbForm
    }
    commands{add_edge e: H -> D1; del_edge e}
  }

  rule shared{
    pattern{H -[deep=REV]-> D; D[Shared=Yes]}
    commands{H.Shared=Yes; del_feat D.Shared}
  }
}

%%%%%%%%%%%%%%%%%%%%%
% Removing the mark REV for reversed relations.
  rule remove_rev {
    pattern { e: M -[deep=REV]-> N }
    commands{del_feat e.deep}
  }

%%%%%%%%%%%%%%%%%%%%%
% Renaming the reversed relations
  package rename_rel {
    rule case-mark {
      pattern { e:M -[case|mark]-> N }
      commands { del_edge e; add_edge M -[comp:obj]-> N }
    }
    rule caus {
      pattern { e:M -[caus]-> N }
      commands { del_edge e; add_edge M -[comp:aux@caus]-> N }
    }

    rule cop {
      pattern { e:M -[cop]-> N }
      commands { del_edge e; add_edge M -[comp:pred]-> N }
    }

    rule pass {
      pattern { e:M -[pass]-> N }
      commands { del_edge e; add_edge M -[comp:aux@pass]-> N }
    }
    
    rule other1 {
      pattern { e:M -[aux]-> N }
      commands { add_edge M -[comp:aux]-> N; del_edge e }
    }
    rule other2 {
      pattern { e:M -[1=aux,2=*]-> N }
      commands { add_edge M -[1=comp,2=aux,deep=e.2]-> N; del_edge e }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
  strat main {
    Seq (
      Onf (normalize_caus_pass),
      Onf (reverse_edges),
      Onf (shift_out_edges),
      Onf (remove_rev),
      Onf (rename_rel),
      )
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Move the dependents of a conjunction from the left conjunct to the right conjunct. Dependencies CONJ, DISCOURSE, PARATAXIS and PUNCT are not moved.
package minimize_conj_dep {
% The shared element directly depends on the head of the first conjunct.
  rule minimize_direct {
    pattern {
      C1 -[1=conj]-> C2;
      e:C1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands { add_edge e:C2 -> D; del_edge e; D.Shared=Yes }
  }
  

% The shared element depends on the head of the first conjunct via a MOD dependency.
  rule minimize_indirect_mod {
    pattern {
      C1 -[1=conj]-> C2;
      C1 -[mod]-> D1; D1 << C2;
      C2 -[mod]-> D2;
      e:D1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands { add_edge e:D2 -> D; del_edge e; D.Shared=Yes }
  }
  
% The shared element depends on the head of the first conjunct via a COMP:OBJ dependency.
  rule minimize_indirect_conj-obj {
    pattern {
      C1 -[1=conj]-> C2;
      C1 -[1=comp,2=obj]-> D1; D1 << C2;
      C2 -[1=comp,2=obj]-> D2;
      e:D1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands { add_edge e:D2 -> D; del_edge e; D.Shared=Yes }
  }
  

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Mark embedded conj relations with the extension "emb".
rule add_conj_emb{
  pattern {
    N1 -[conj|conj:emb]-> N2;
    e: N2 -[conj]-> N3;
  }
  commands {
    del_edge e; add_edge N2 -[conj:emb]-> N3;
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Dependencies of type CONJ, FIXED and FLAT:NAME grouped into a bouquet are reorganized into a chain.
package chained_relations {
  rule conj {
    pattern {H -[1=conj]-> D1;e: H -[1=conj]-> D2;D1 << D2}
    without {H -[1=conj]-> D;D1 << D; D << D2}
    without {H -[1=conj]-> D;D2 << D}
    commands {add_edge e:D1 -> D2;del_edge e;}
  }

  rule fixed {
    pattern {
      H -[unk]-> D1; * -[IDIOM]-> D1;
      e: H -[unk]-> D2;* -[IDIOM]-> D2;D1 << D2}
    without {H -[unk]-> D;* -[IDIOM]-> D;D1 << D; D << D2}
    without {H -[unk]-> D;* -[IDIOM]-> D;D2 << D;}
    commands {del_edge e;add_edge D1 -[unk]-> D2}
  }

  rule flat_name {
    pattern {H -[flat@name]-> D1;e: H -[flat@name]-> D2;D1 << D2}
    without {H -[flat@name]-> D;D1 << D; D << D2}
    without {H -[flat@name]-> D;D2 << D}
    commands {del_edge e;add_edge D1 -[flat@name]-> D2}
  }

  rule flat_foreign {
    pattern {H -[flat@foreign]-> D1;e: H -[flat@foreign]-> D2;D1 << D2}
    without {H -[flat@foreign]-> D;D1 << D; D << D2}
    without {H -[flat@foreign]-> D;D2 << D}
    commands {del_edge e;add_edge D1 -[flat@foreign]-> D2}
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
rule remove_useless_emb {
  pattern { e: M -[conj:emb]-> N }
  without { _ -[1=conj]-> M }
  commands { del_feat e.2 }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat main {
  Seq (
    Onf (eud_to_ud),
    specific_expr_init, % Initialization of a specific treatment for MWE, titles and translated words.
    Onf (relations), % Package RELATIONS must precede relations reverse because the renaming of some relations depends on the presence of other relations (nsubj, csubj)
    reverse_relations.main, % Reversing aux, case, cop and mark relations.
    Onf (minimize_conj_dep), % Package MINIMIZE_CONJ_DEP must follow strategy REVERSE_RELATIONS.MAIN because the shared dependents can depend on relation inversions.
    Onf (sub_relations), % Package SUB_RELATIONS must precede package REL_EXTENSIONS because this package swat extensions on level 2.
    Onf (rel_extensions),
    Onf (add_conj_emb), % Package ADD_CONJ_EMB must follow REVERSE_RELATIONS.MAIN because it depends on head changes ; Ex: UD_French-GSD fr-ud-dev_00751
    Onf (chained_relations),% Package CHAINED_RELATIONS must follow package SHIFT_RULES because it can entail a wrong coordination between two elements because of errors in head changes (UD_French-GS: fr-ud-dev_00131).
    specific_expr_close, % Closing a specific treatment for MWE, titles and translated words.
    Onf(remove_useless_emb),
  )
}
