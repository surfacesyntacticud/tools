%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "UD_to_SUD.grs"


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Relations in SUD that require the use of a lexicon to be determined from UD relations.
package FR_relations {

% When a NUM D depends on a word G in a NUMMOD relation and when D precedes G, D is a determiner of G.
  rule num_det{
    pattern{
      D[upos=NUM];
      e:H -[nummod]-> D; D << H
    }
    commands{ del_edge e; add_edge H -[det]-> D}
  }

% The subject SUBJ1 of a verb in the active mood is repeated in the form of a personal pronoun SUBJ2.
% Ex: Jean vient-il ?
  rule repeat_subj{
    pattern{
     e1:V -[csubj|nsubj]->  SUBJ1; SUBJ1 << V;
     e2:V -[expl:subj]-> SUBJ2; SUBJ2[upos=PRON, lemma=il]; SUBJ2 >> V}
    commands{
      del_edge e1; add_edge V -[subj]-> SUBJ1;
      del_edge e2; add_edge V -[subj@expl]-> SUBJ2;}
  }
  
% The subject SUBJ1 of a verb in the passive mood is repeated in the form of a personal pronoun SUBJ2.
% Ex: Jean a-t-il été contacté par Pierre ?
  rule repeat_subj_passive{
    pattern{
     e1:V -[csubj:pass|nsubj:pass]->  SUBJ1; SUBJ1 << V;
     e2:V -[expl:subj]-> SUBJ2; SUBJ2[upos=PRON, lemma=il]; SUBJ2 >> V}
    commands{
      del_edge e1; add_edge V -[subj@pass]-> SUBJ1;
      del_edge e2; add_edge V -[subj@expl]-> SUBJ2;}
  }
  
 % Relation between a verb and an argument in the form of a finite clause, which is an oblique complement introduced with "que". By default, CCOMP relations are transformed into COMP:OBJ relations. In this case, they are transformed into COMP:OBL relations.
  rule verb_compobl_ccomp (lex from "lexicons/verb_with_deobj_fin-verb.lp") {
    pattern {
      H[upos=VERB,lemma=lex.verb]; e:H -[ccomp]-> C;
      C[upos=SCONJ,lemma=que]}
    commands { del_edge e; add_edge H -[comp:obl]-> C }
  }

% Relation between a verb with affix and an argument in the form of a finite clause, which is an oblique complement introduced with "que". By default, CCOMP relations are transformed into COMP:OBJ relations. In this case, they are transformed into COMP:OBL relations.
  rule verb_aff_compobl_ccomp (lex from "lexicons/aff_verb_with_deobj_fin-verb.lp") {
    pattern {
      H[upos=VERB,lemma=lex.verb]; e:H -[ccomp]-> C;
      C[upos=SCONJ,lemma=que];
      AFF[upos=PRON, lemma=lex.aff]; H -[expl]-> AFF}
    commands { del_edge e; add_edge H -[comp:obl]-> C }
  }

}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat FR_core{
  Seq (
%    Onf (minimize_conj_dep),
    Onf (head_reverse),
    Onf (gov_rise),
    Onf (FR_relations),
    Onf (sub_relations),
    Onf (rel_extensions),
    Onf (relations),
    Onf (chained_relations),
  )
}

strat FR_main {
  Seq(
    specific_expr_init, % Initialization of a specific treatment for MWE, titles and translated words.
    FR_core,
    specific_expr_close, % Closing a specific treatment for MWE, titles and translated words.
    Onf(add_deep_fixed), % in SUD, INMWE becomes @fixed
  )
}

