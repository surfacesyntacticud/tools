%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat mSUD_to_SUD_main { 
  Seq (
    % Onf (up_type_m),
    Onf (unify_DerPos_CpdPos),
    Onf (fusion),
  )
}

package unify_DerPos_CpdPos {
  rule DerPos {
    pattern { X [DerPos] }
    commands { X.FinalPos=X.DerPos; del_feat X.DerPos }
  }
  rule CpdPos {
    pattern { X [CpdPos] }
    commands { X.FinalPos=X.CpdPos; del_feat X.CpdPos }
  }
}


package fusion {
% The rule [up_type_m] ensures that there is word-level dependency above a morph-level dependency
% If such a pattern in found, the relation goes up in the tree
rule up_type_m {
  pattern { 
    N1 -[type=m]-> N2;
    e: N2 -[!type]-> N3;
  }
  commands {
    add_edge f:N1 -> N3;
    f.label = e.label;
    del_edge e
  }
}

  rule suff_fusion_FinalPos {
    pattern {
      N[lemma]; M[lemma];
      N [FinalPos];
      e: N -[type=m]-> M;                                  % M is attached with the /m extension
      N < M                                                % M is a suffix
    }  
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      del_edge e;
      N.form = N.form + M.form;                            % concatenate the surface form
      N.wordform = N.wordform + M.wordform;                % concatenate the surface form
      N.lemma = N.lemma + M.lemma;                         % concatenate the surface lemma
      append_feats "_" M =[re"Translit\|Tone"]=> N;        % concatenate the surface Translit and Tone
      N.upos = N.FinalPos; 
      del_feat N.FinalPos;
      del_node M;
    }
  }

  rule suff_fusion {
    pattern {
      P[lemma]; Q[lemma];
      P[!FinalPos];
      P -[type=m]-> Q;                                     % Q is attached with the /m extension
      P < Q                                                % Q is a suffix
    }  
    without { Q -> X}                                      % Q is a leaf (force application order)
    commands { 
      P.form = P.form + Q.form;                            % concatenate the surface form
      P.wordform = P.wordform + Q.wordform;                % concatenate the surface form
      P.lemma = P.lemma + Q.lemma;                         % concatenate the surface lemma
      append_feats "_" Q =[re"Translit\|Tone"]=> P;        % concatenate the surface Translit and Tone
      del_node Q;
    }
  }
  
  rule pref_fusion_FinalPos {
    pattern {
      N[lemma]; M[lemma];
      N [FinalPos];
      N -[type=m]-> M;                                     % M is attached with the /m extension
      M < N                                                % M is a prefix
    }  
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      N.form = M.form + N.form;                            % concatenate the surface form
      N.wordform = M.wordform + N.wordform;                % concatenate the surface form
      N.lemma = M.lemma + N.lemma;                         % concatenate the surface lemma
      prepend_feats "_" M =[re"Translit\|Tone"]=> N;       % concatenate the surface Translit and Tone
      del_node M;
      N.upos = N.FinalPos; 
      del_feat N.FinalPos;
    }
  }

  rule pref_fusion {
    pattern { 
      N[lemma]; M[lemma];
      N [!FinalPos];
      N -[type=m]-> M;                                     % M is attached with the /m extension
      M < N                                                % M is a prefix
    }  
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      N.form = M.form + N.form;                            % concatenate the surface form
      N.wordform = M.wordform + N.wordform;                % concatenate the surface form
      N.lemma = M.lemma + N.lemma;                         % concatenate the surface lemma
      prepend_feats "_" M =[re"Translit\|Tone"]=> N;       % concatenate the surface Translit and Tone
      del_node M;
    }
  }

  rule suff_fusion_FinalPos_without_lemmas {
    pattern {
      N[]; M[];
      N [FinalPos];
      e: N -[type=m]-> M;                                  % M is attached with the /m extension
      N < M                                                % M is a suffix
    }
    without { N[lemma]; M[lemma] }
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      del_edge e;
      N.form = N.form + M.form;                            % concatenate the surface form
      N.wordform = N.wordform + M.wordform;                % concatenate the surface form
      append_feats "_" M =[re"Translit\|Tone"]=> N;        % concatenate the surface Translit and Tone
      N.upos = N.FinalPos; 
      del_feat N.FinalPos;
      del_node M;
    }
  }

  rule suff_fusion_without_lemmas {
    pattern {
      P[]; Q[];
      P[!FinalPos];
      P -[type=m]-> Q;                                     % Q is attached with the /m extension
      P < Q                                                % Q is a suffix
    }  
    without { P[lemma]; Q[lemma] }
    without { Q -> X}                                      % Q is a leaf (force application order)
    commands { 
      P.form = P.form + Q.form;                            % concatenate the surface form
      %P.wordform = P.wordform + Q.wordform;                % concatenate the surface form
      append_feats "_" Q =[re"Translit\|Tone"]=> P;        % concatenate the surface Translit and Tone
      del_node Q;
    }
  }
  
  rule pref_fusion_FinalPos_without_lemmas {
    pattern {
      N[]; M[];
      N [FinalPos];
      N -[type=m]-> M;                                     % M is attached with the /m extension
      M < N                                                % M is a prefix
    }  
    without { N[lemma]; M[lemma] }
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      N.form = M.form + N.form;                            % concatenate the surface form
      N.wordform = M.wordform + N.wordform;                % concatenate the surface form
      prepend_feats "_" M =[re"Translit\|Tone"]=> N;       % concatenate the surface Translit and Tone
      del_node M;
      N.upos = N.FinalPos; 
      del_feat N.FinalPos;
    }
  }

  rule pref_fusion_without_lemmas {
    pattern { 
      N[]; M[];
      N [!FinalPos];
      N -[type=m]-> M;                                     % M is attached with the /m extension
      M < N                                                % M is a prefix
    }  
    without { N[lemma]; M[lemma] }
    without { M -> X}                                      % M is a leaf (force application order)
    commands { 
      N.form = M.form + N.form;                            % concatenate the surface form
      N.wordform = M.wordform + N.wordform;                % concatenate the surface form
      prepend_feats "_" M =[re"Translit\|Tone"]=> N;       % concatenate the surface Translit and Tone
      del_node M;
    }
  }


  rule missing_m_pref_fwd {
    pattern { X[form=re".+-"]; Y[]; X < Y; e: X -[!type]-> Y; }
    commands { e.type=m }
  }
  rule missing_m_pref_bck {
    pattern { X[form=re".+-"]; Y[]; X < Y; e: Y -[!type]-> X; }
    commands { e.type=m }
  }
  rule missing_m_suff_fwd {
    pattern { X[]; Y[form = re"-.+"]; X < Y; e: X -[!type]-> Y; }
    commands { e.type=m }
  }
  rule missing_m_suff_bck {
    pattern { X[]; Y[form = re"-.+"]; X < Y; e: Y -[!type]-> X; }
    commands { e.type=m }
  }
}

