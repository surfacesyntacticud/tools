%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "SUD_to_UD.grs"

% The conversion of "suite à", "face à" are invalid ('advmod' should be 'ADV' but it is 'NOUN')
% To avoid this, we transform these phrase as idiom in order to make them fixed after conversion.
package FR_init {
  rule prep_loc {
    pattern {
      N1 [lemma=lex.noun, upos=NOUN, ExtPos=ADV];
      N2 [lemma=lex.prep];
      e:N1 -[comp:obl]-> N2
    }
    commands {
      N1.ExtPos = ADP; N1.Idiom = Yes;
      N2.InIdiom = Yes;
      del_edge e; add_edge N1 -[comp:obj]-> N2
    }
  }
#BEGIN lex
noun	prep
%-------------
face	à
grâce	à
suite	à
histoire	de
#END

  % New validation rule in 2024: a det should not have a dep
  % SUD annotation of tel point que / telle façon que are invalid
  % Changed before conversion
  rule no_child_on_det {
    pattern { N -[det]-> X; X [upos=DET]; e: X -[comp:obj]-> CL }
    commands {
      del_edge e;
      add_edge N -[comp:obj]-> CL;
    }
  }

  rule no_extpos_on_num {
    pattern { X[upos=NUM, ExtPos] }
    commands { del_feat X.ExtPos }
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% French pronominal verbs are considered as idioms. In the conversion to UD, their idiomatic characteristic is removed.
package FR_pron_verb{
  % The pronominal verb has a unique clitic pronoun marking its pronominal feature.
  % Ex: les gens s'enfuient.
  rule verb1{
    pattern{
      V[upos=VERB,ExtPos=VERB, Idiom=Yes];
      V -[comp]-> PRON; PRON [upos=PRON,InIdiom=Yes]}
    without{V -[comp]-> PRON1; PRON1 [upos=PRON,InIdiom=Yes]}
    commands{
      del_feat V.ExtPos; del_feat  V.Idiom;
      del_feat PRON.InIdiom}
  }

  % The pronominal verb has two clitic pronouns marking its pronominal feature.
  % Ex: ils s'en prennent aux plus faibles.
  rule verb2{
    pattern{
      V[upos=VERB,ExtPos=VERB, Idiom=Yes];
      V -[comp]-> PRON1; PRON1 [upos=PRON,InIdiom=Yes];
      V -[comp]-> PRON2; PRON2 [upos=PRON,InIdiom=Yes]; PRON1 << PRON2}
    commands{
      del_feat V.ExtPos; del_feat  V.Idiom;
      del_feat PRON1.InIdiom; del_feat PRON2.InIdiom;}
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of MOD relations specific to French
package FR_mod {
  % Adjectival, verbal or nominal expressions that are hightlighted at the beginning of a clause are considered as incomplete clauses.
  % Ex: fr-ud-dev_00407 - Spécialiste des six jours, il a remporté 38 compétitions entre 1931 et 1942.
  rule left_extraposition(lex from "lexicons/noun_adv.lp") {
    pattern{
      APP[upos=ADJ|NOUN|PROPN|VERB];
      V -[1=subj]-> SUBJ; APP << SUBJ; SUBJ << V;
      e: V -[mod]-> APP}
    without{ APP -[1=case]-> *}
    without{APP[upos=NOUN]; APP -[det]-> *}
    without{APP[upos=NOUN,lemma= lex.noun]}
    without{% Modifiers in the form NOUN + PREP + NOUN.
            % Ex: fr-ud-dev_00322 - Couche par couche, Morin arrange ...
      APP[upos=NOUN];
      P[upos=ADP,lemma="après"|par|sur];
      N[upos=NOUN];
      N -[1=case]-> P;
      APP -[1=udep]-> N;
      N.lemma=APP.lemma
    }
    commands{ del_edge e; add_edge V -[advcl]-> APP}
  }

  % Nouns behaving as circumstancial complements and heads of modifiers in the form of adverbial clauses.
  % Ex: Il pleut cette année comme l'an dernier.
  rule conj_adv-noun(lex from "lexicons/noun_adv.lp") {
    pattern{
      N[upos=NOUN,lemma=lex.noun];
      e: N -[1=case]-> C; C[upos=SCONJ];
      * -[mod]-> N
    }
    commands{e.1=mark}
  }

  % Numbers that follow a measure unity
  rule unity_num(lex from "lexicons/noun_measure_unit.lp"){
    pattern{D[upos=NUM]; H[lemma=lex.unity]; e:H -[mod]-> D }
    commands{del_edge e; add_edge H -[nummod]-> D}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of UDEP relations specific to French
package FR_udep {
  % Expressions in the form "quelque/grand chose de" + ADJ
  rule chose_de_adj{
    pattern{
      H[upos=PRON]; D[lemma=chose]; H -[HEAD]-> D;
      A[upos=ADJ|DET,lemma=grand|quelque]; D -[det|mod]-> A;
      D1[upos=ADJ];e: H -[mod]-> D1;
      D1 -[1=case]-> DE; DE[upos=ADP,lemma=de]
    }
    commands{ del_edge e; add_edge H -[amod]-> D1}
  }

  % Expressions in the form "rien de" + ADJ
  rule rien_de_adj{
    pattern{
      H[upos=NOUN|PRON,lemma=rien]; D[upos=ADJ]; e: H -[mod]-> D;
      D -[1=case]-> DE; DE[upos=ADP,lemma=de]
    }
    commands{ del_edge e; add_edge H -[amod]-> D}
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of subject relations specific to French.
package FR_subj {
  % Subjects of passive participles without auxiliary.
  rule nsubj_pass (lex from "lexicons/verb_passivable.lp") {
    pattern {e:V -[nsubj]-> S; V [VerbForm=Part,Tense=Past,lemma=lex.verb] }
    without {* -[1=aux]-> V}
    commands { del_edge e; add_edge V -[nsubj:pass]-> S }
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Main strategy for converting any SUD French annotation into a UDplus annotation.
% The UDplus schema is in accordance the UD schema with additional information.
% It distinguishes between two types of idioms: idioms without syntactic structure are annotated with fixed relations according to the UD guidelines;
% idioms with a syntactic structure are annotated like any ordinary expression but their head is marked  with the feature Idiom=Yes;
% it also contains a feature ExtPos, which denotes the element’s function within the wider sentence; the other components of the idiom contain the feature InIdiom=Yes.
% For the sake of uniformity, the use of features Idiom, ExtPos an InIdiom is extended to idioms without syntactic structure.
strat FR_main_UDplus{
  Seq(
    Onf (FR_init), % Needed for UD validation
    Onf (init),
    Onf (conjdicto),
    Onf (shared),
    Onf (remove_shared),
    Onf (FR_pron_verb), % Specific treatment of pronominal verbs in French
    specific_expr_init,
    Onf (nom_init),
    Onf (unchained_relations),
    Onf (reverse_case_mark),
    Onf (shift_out_edges),
    Onf (case_mark_relations),
    Onf (reverse_close),
    Onf (sub_relations),
    Onf (FR_mod),
    Onf (FR_udep),
    Onf (rel_extensions),
    Onf (relations),
    Onf (relations_backoff),
    Onf (FR_subj),
    Onf (reverse_aux),
    Onf (shift_out_edges),
    Onf (reverse_close),
    Onf(outer_copula),
    Onf (final),
    specific_expr_close,
    Onf(extpos),
    Onf(FR_final) % avoid UD validation errors
  )
}

package FR_final {
  rule ExtPos_lang {
    pattern { N [ExtPos, Lang] }
    commands { del_feat N.ExtPos }
  }

  rule dislocated {
    pattern { e: X -[1=dislocated, 2=*]-> Y }
    commands { del_feat e.2 }
  }
}

% In GSD, there are 3 discontinuous MWEs (in parenthesis, the insered span):
% * [fr-ud-train_07306] …et face (à la fois) aux Alliés…
% * [fr-ud-train_11812] …grâce (aussi) à leur accueil…
% * [fr-ud-train_14502] …suite (notamment) à ses nombreuses visites…
% In order to have a valid UD annotation, a post-treatment is needed, hence the next rule
rule FR_discontinuous_MWE {
  pattern {
    H -[case]-> N1; N1 [lemma="suite"|"face"|"grâce"]; e: N1 -[dep:comp]-> N2
  }
  commands {
    del_edge e;
    add_edge H -[case]-> N2;
  }
}

rule FR_spoken_tilde {
  pattern { N [form=re".*~", ExtPos] }
  commands {
    N.upos = N.ExtPos;
    del_feat N.ExtPos
  }
}

package remove_Cxn { 
  rule Cxn {
    pattern { X [Cxn] }
    commands { del_feat X.Cxn }
  }
  rule CxnElt {
    pattern { X [CxnElt] }
    commands { del_feat X.CxnElt }
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Main strategy for converting any SUD French annotation into a UD annotation.
strat fr_main{
  Seq (
    FR_main_UDplus,
    flatten_idioms,
    Onf(remove_UDplus_subtypes),
    Onf(fix_punct),
    Onf(FR_discontinuous_MWE),
    Onf(FR_spoken_tilde),
    Onf(remove_Cxn),
  )
}
