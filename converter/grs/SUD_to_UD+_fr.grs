%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "SUD_to_UD+.grs"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% French pronominal verbs are considered as idioms. In the conversion to UD, their idiomatic characteristic is removed.
package FR_pron_verb{
% The pronominal verb has a unique clitic pronoun marking its pronominal feature.
% Ex: les gens s'enfuient.
  rule verb1{
    pattern{
      V[upos=VERB,ExtPos=VERB, PhraseType=Idiom];
      V -[comp]-> PRON; PRON [upos=PRON,InIdiom=Yes]}
    without{V -[comp]-> PRON1; PRON1 [upos=PRON,InIdiom=Yes]}
    commands{
      del_feat V.ExtPos; del_feat  V.PhraseType;
      del_feat PRON.InIdiom}
  }

% The pronominal verb has two clitic pronouns marking its pronominal feature.
% Ex: ils s'en prennent aux plus faibles.
  rule verb2{
    pattern{
      V[upos=VERB,ExtPos=VERB, PhraseType=Idiom];
      V -[comp]-> PRON1; PRON1 [upos=PRON,InIdiom=Yes];
      V -[comp]-> PRON2; PRON2 [upos=PRON,InIdiom=Yes]; PRON1 << PRON2}
    commands{
      del_feat V.ExtPos; del_feat  V.PhraseType;
      del_feat PRON1.InIdiom; del_feat PRON2.InIdiom;}
  }
}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of MOD relations specific to French
package FR_mod {
% Adjectival, verbal or nominal expressions that are hightlighted at the beginning of a clause are considered as incomplete clauses.
% Ex: fr-ud-dev_00407 - Spécialiste des six jours, il a remporté 38 compétitions entre 1931 et 1942.
  rule left_extraposition(lex from "lexicons/noun_adv.lp") {
    pattern{
      APP[upos=ADJ|NOUN|PRON|PROPN|VERB];
      V -[1=subj]-> SUBJ; APP << SUBJ; SUBJ << V;
      e: V -[mod]-> APP}
    without{ APP -[1=case]-> *}
    without{APP[upos=NOUN]; APP -[det]-> *}
    without{APP[upos=NOUN,lemma= lex.noun]}
    without{% Modifiers in the form NOUN + PREP + NOUN.
            % Ex: fr-ud-dev_00322 - Couche par couche, Morin arrange ...
      APP[upos=NOUN];
      P[upos=ADP,lemma="après"|par|sur];
      N[upos=NOUN];
      N -[1=case]-> P;
      APP -[1=udep]-> N;
      N.lemma=APP.lemma
    }
    commands{ del_edge e; add_edge V -[advcl]-> APP}
  }

% Nouns behaving as circumstancial complements and heads of modifiers in the form of adverbial clauses.
% Ex: Il pleut cette année comme l'an dernier.
  rule conj_adv-noun(lex from "lexicons/noun_adv.lp") {
    pattern{
      N[upos=NOUN,lemma=lex.noun];
      e: N -[1=case]-> C; C[upos=SCONJ];
      * -[mod]-> N
    }
    commands{e.1=mark}
  }

% Numbers that follow a measure unity
  rule unity_num(lex from "lexicons/noun_measure_unit.lp"){
    pattern{D[upos=NUM]; H[lemma=lex.unity]; e:H -[mod]-> D }
    commands{del_edge e; add_edge H -[nummod]-> D}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of UDEP relations specific to French
package FR_udep {
% Expressions in the form "quelque/grand chose de" + ADJ
  rule chose_de_adj{
    pattern{
      H[upos=PRON]; D[lemma=chose]; H -[HEAD]-> D;
      A[upos=ADJ|DET,lemma=grand|quelque]; D -[det|mod]-> A;
      D1[upos=ADJ];e: H -[mod]-> D1;
      D1 -[1=case]-> DE; DE[upos=ADP,lemma=de]
    }
    commands{ del_edge e; add_edge H -[amod]-> D1}
  }

% Expressions in the form "rien de" + ADJ
  rule rien_de_adj{
    pattern{
      H[upos=NOUN|PRON,lemma=rien]; D[upos=ADJ]; e: H -[mod]-> D;
      D -[1=case]-> DE; DE[upos=ADP,lemma=de]
    }
    commands{ del_edge e; add_edge H -[amod]-> D}
  }

}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of subject relations specific to French.
package FR_subj {
% Subjects of passive participles without auxiliary.
  rule nsubj_pass (lex from "lexicons/verb_passivable.lp") {
    pattern {e:V -[nsubj]-> S; V [VerbForm=Part,Tense=Past,lemma=lex.verb] }
    without {* -[1=aux]-> V}
    commands { del_edge e; add_edge V -[nsubj:pass]-> S }
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat FR_main{
  Seq(
    Onf (shared),
    Onf(remove_shared),
    Onf(FR_pron_verb), % Specific treatment of pronominal verbs in French
    specific_expr_init,
    Onf (nom_init),
    Onf (unchained_relations),
    Onf (reverse_case_mark),
    Onf(shift_out_edges),
    Onf(case_mark_relations),
    Onf( reverse_close),
    Onf (sub_relations),
    Onf (FR_mod),
    Onf (FR_udep),
    Onf (rel_extensions),
    Onf (relations),
    Onf (relations_backoff),
    Onf (FR_subj),
    Onf(reverse_aux),
    Onf(shift_out_edges),
    Onf( reverse_close),
    Onf (final),
    specific_expr_close,
  )
}


