%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "SUD_to_UD.grs"
include "zh_mSUD_to_SUD.grs"

strat zh_mSUD_to_UD_main { 
  Seq (
    zh_mSUD_to_SUD_main,
    Onf (comp),
    u_main,
    Onf (zh_post)
  )
}

package comp {
  rule obl {
    pattern { 
      e: N -[comp]-> M; N[form <> "的"];
      M [upos=ADP|PART];
    }
    commands { e.2 = obl }
  }

  rule obj {
    pattern { 
      e: N -[comp]-> M; N[form <> "的"];
      M [upos=NOUN|PROPN|PRON|VERB|AUX];
    }
    commands { e.2 = obj }
  }

  rule obj2 {
    pattern { 
      e: N -[comp]-> M; N[form = "的"];
    }
    commands { e.2 = obj }
  }
}

package zh_post {
  rule obl_mod {
    pattern {
      e: N -[obl:mod]-> M;
    }
    commands {
      del_feat e.2;
    }
  }

  rule dep_comp {
    pattern {
      e: N -[dep:comp]-> M;
    }
    commands {
      del_feat e.2;
    }
  }
}