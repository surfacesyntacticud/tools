%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The "irregular_expr.grs" file includes packages applying a specific treatment to idioms, titles and translated. What they have in common is the fact that their syntactic behaviour towards the outside world is not regular.
include "irregular_expr.grs"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Remove all information specific to Enhanced UD.
package eud_to_ud {
  rule deep { % remove add enhanced relations
    pattern { e:N -[enhanced=yes]-> M }
    commands { del_edge e}
  }

  rule empty { % remove empty nodes
    pattern { N [wordform=__EMPTY__, textform=_] }
    commands { del_node N }
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Marking the elements of idioms without syntactic structure.
package idioms{
% Marking the heads. We make the hypothesis that the idiom has the same POS as its head.
  rule idiom_head{
    pattern{H -[fixed]-> *; H[!ExtPos,!PhraseType]}
    commands{H.ExtPos=H.upos; H.PhraseType=Idiom}
  }

% Marking the dependents.
  rule idiom_dependent{
    pattern{* -[fixed]-> D; D[!InIdiom]}
    commands{D.InIdiom=Yes}
  }
  
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD sub-relations are renamed as SUD sub-relations or relations.
% The rules are arranged in the alphabetic order of the SUD resulting relations or sub-relations.
package sub_relations {
% comp
%=========
% Sub-relations DEP:COMP are renamed COMP in SUD if they do not start from nominals.
  rule comp_dep {
    pattern { H[upos <> NOUN|PRON]; e:H -[dep:comp]-> D; }
    commands {del_edge e; add_edge H -[comp]-> D}
  }

% Clitic pronouns expressing the mediopassive redistribution.
% Ex: les maisons se vendent bien en ce moment.
  rule expl-pass_comp-pass{
    pattern{e: H -[expl:pass]-> D}
    commands{del_edge e; add_edge H -[comp@pass]-> D}
  }

% Clausal direct objects or oblique arguments with their subject controlled by their governor.
  rule comp-obj-obl_xcomp {
    pattern { e:H -[xcomp:obj|xcomp:obl]-> D; }
    commands {e.1=comp;e.deep=x}
  }

% All sub-relations of COMP, for which the extension does not change from UD to SUD.
  rule comp {
    pattern { e:H -[advcl:cleft|ccomp:obj|ccomp:obl|ccomp:pred]-> D; }
    commands {e.1=comp}
  }

% In the impersonal diathesis, the deep subject, which is a NSUBJ or CSUBJ dependent in UD, becomes a direct object in SUD with the deep extension AGENT.
% The impersonal diathesis is detected by means of the impersonal subject.
% The UD relation that is renamed as a SUD relation is not a sub-relation but it must be processed before the general processing of relations.
% Ex: Il arrive deux personnes.  - Il m'arrive de faire des erreurs.
  rule comp-obj-agent_subj {
    pattern{e:H -[nsubj|csubj]-> D; H -[expl:subj]-> *}
    commands{del_edge e; add_edge H -[comp:obj@agent]-> D }
  }

% In the impersonal passive diathesis, the deep subject, which is a NSUBJ:PASS or CSUBJ:PASS dependent in UD, becomes an OBJ:PASS dependent in SUD.
% The impersonal diathesis is detected by means of the impersonal subject,which may not be a direct dependent of the main verb, hence the need for several rules.
% Ex: Il sera vendu une grande maison aujourd'hui - Il est prévu de continuer. - Il a été vendu une grande maison aujourd'hui. - Il a été prévu de continuer. - Il peut avoir été vendu une grande maison aujourd'hui.- Il se dit que tu viendras.  Il s'est vendu une grande maison aujourd'hui. - Il a pu se vendre une grande maison aujourd'hui.
  rule comp-obj-pass_subj {
    pattern{e:H -[1=csubj|nsubj, 2=pass]-> D; H -[expl:subj]-> *}
    commands{del_edge e; add_edge H -[comp:obj@pass]-> D; }
  }

% Oblique argument.
  rule comp_obl {
    pattern { e:H -[1=obl, 2=arg|comp]-> D; } % comp pour Spoken
    commands {e.1=comp; e.2=obl}
  }

% Agent complement.
  rule comp_obl-agent {
    pattern { e:H -[1=obl, 2=agent]-> D}
    commands {del_edge e; add_edge H -[comp:obl@agent]-> D}
  }

% Sub-relations DEP:COMP starting from nominals are renamed COMP:OBL in SUD. We make the hypothesis that all arguments of nominals are oblique arguments.
  rule comp_dep_nom {
    pattern { H[upos = NOUN|PRON]; e:H -[dep:comp]-> D; }
    commands {del_edge e; add_edge H -[comp:obl]-> D}
  }

% Predicative non clausal argument.
  rule comp-pred_xcomp {
    pattern { e:H -[xcomp:pred]-> D}
    without{D[upos=AUX|VERB] }
    without{D -[cop]-> *}
    commands {del_edge e; add_edge H -[comp:pred]-> D}
  }

% Predicative clausal argument without control of its subject.
  rule comp-pred_ccomp {
    pattern { e:H -[ccomp:pred]-> D; }
    commands {e.1=comp; e.2=pred}
  }

% Predicative clausal argument with control of the subject by the main verb.
  rule comp-pred-x_xcomp {
    pattern { e:H -[xcomp:pred]-> D;D[upos=VERB] }
    commands {del_edge e; add_edge H -[comp:pred@x]-> D}
  }

% Predicative clausal argument with a copula; the copula subject is controlled by the main verb.
  rule comp-pred-x_xcomp-cop {
    pattern { e:H -[xcomp:pred]-> D;D -[cop]-> D1 }
    commands {del_edge e; add_edge H -[comp:pred@x]-> D}
  }


% mod
%=========

 % Oblique modifier.
  rule mod {
    pattern { e:H -[obl:mod]-> D; }
    commands {e.1=mod; del_feat e.2}
  }

   rule mod-relcl{
     pattern { e:H -[acl:relcl]-> D; }
     commands {del_edge e; add_edge H -[mod@relcl]-> D}
   }


% subj
%=========
% In an impersonal construction, the impersonal subject, which is an EXPL dependent in UD, becomes an  explicit subject in SUD with the deep extension expl.
% Ex: Il arrive deux personnes. - Il faut un bilan. - Il s'agit d'une erreur. - Il a été vendu une grande maison. - il se dit que tu viendras.
  rule subj_expl {
    pattern { e:H -[expl:subj]-> D; }
    without{H -[csubj|nsubj|csubj:pass|nsubj:pass]-> *}
    commands {e.1=subj;e.deep=expl; del_feat e.2}
  }


% unk
%=========
  rule other_expl {
    pattern { e:H -[1=expl, 2<>subj|comp|pass]-> D; }
    commands {del_edge e; add_edge H -[unk@expl]-> D}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD extensions of relations are transformed into deep extensions of SUD relations and deep extensions "reversed" are deleted.
% Examples of cop extensions: cop:locat in UD_Polish-LFG and cop:own in UD_Finnish-* and UD_Karelian-KKPP
package rel_extensions {
% When a relation with an extension is different from COMP, its extension is transformed into a deep extension.
  rule change_ext{
    pattern{e: H -[1=*,2=*]-> D}
    without{ H -[1=aux|comp|subj]-> D}
    without{ H -[conj:emb]-> D}
    commands{ add_edge H -[1=e.1,deep=e.2]-> D; del_edge e}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% UD relations are renamed as SUD relations or sub-relations.
% Rem: the order of the rules follows the alphabetic order of the names of the resulting relations.
package relations {


%comp:obj
%=========
  rule comp-obj_obj {
    pattern { e:H -[1=obj]-> D; }
    commands { e.1=comp;e.2=obj }
  }

% Direct clausal argument of a verb without control of its subject. We make the assumption that a clausal argument that is not introduced with an adposition is a direct object.
  rule comp-obj_ccomp {
    pattern {e:H -[1=ccomp]-> D; H[upos=VERB]}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    without{H -[obj|comp:obj]-> *}% There is no other direct object.
    without{H -[deep=agent]-> D}% Ex: Il est gêné que tu viennes.
    without{ D -[mark]-> ADP; ADP[upos=ADP]} % The complement is not marked with an adposition
    without{H -[aux@pass|comp:aux@pass]-> *}% A passive verb has no direct object.
    commands{e.1=comp;e.2=obj}
  }

% Direct clausal argument of a verb with control of its subject. We make the assumption that a clausal argument that is not introduced with an adposition is a direct object.
  rule comp-obj_xcomp {
    pattern { e:H -[1=xcomp]-> D; H[upos=VERB]; D[upos=VERB]}
    without{D -[cop]-> *}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    without{H -[obj|comp:obj]-> *}% There is no other direct object.
    without{ D -[mark]-> ADP; ADP[upos=ADP]}
    without{H -[comp:aux@pass|aux@pass]-> *}% A passive verb has no direct object.
    commands {e.1=comp;e.2=obj;e.deep=x}
  }

% Direct clausal argument of a verb with a copula: the coupla subject is controlled by the main verb. We make the assumption that a clausal argument that is not introduced with an adposition is a direct object.
% Ex: Il veut être soldat.
  rule comp-obj_xcomp-cop {
    pattern { e:H -[1=xcomp]-> D; H[upos=VERB];D -[cop]-> D1}
    without{H -[obj|comp:obj]-> *}% There is no other direct object.
    without{ D -[mark]-> ADP; ADP[upos=ADP]}
    without{H -[comp:aux@pass|aux@pass]-> *}% A passive verb has no direct object.
    commands {e.1=comp;e.2=obj;e.deep=x}
  }


% comp:obl
%=========
% Oblique clausal argument of a verb without control of its subject.
% Ex: C'est facile à faire.
  rule comp-obl_ccomp {
    pattern { e:H -[1=ccomp]-> D;  H[upos=VERB]; D -[mark]-> ADP; ADP[upos=ADP]}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl}
  }

% Oblique clausal argument of a non verbal predicate without control of its subject. We make the assumption that all clausal arguments of non verbal predicates are oblique arguments.
% Ex: Il est heureux que Jean soit venu.
  rule comp-obl_ccomp-non-verb {
    pattern { e:H -[1=ccomp]-> D; H[upos<>VERB]; }
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl}
  }

% Clausal agent complements.
%Ex: fr-ud-train_03013 -  La fédération italienne se dit choquée que le joueur français ait été choisi
  rule comp-obl_ccomp-agent {
    pattern {e:H -[1=ccomp,deep=agent]-> D}
    commands {e.1=comp;e.2=obl}
  }

% Clausal arguments in the presence of a direct object are considered as oblique arguments.
% Je convainc Marc qu'il doit venir.
  rule comp-obl_ccomp-obj {
    pattern {
      e:H -[1=ccomp]-> D;
      H -[obj|comp:obj]-> D1}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl}
  }

% Clausal arguments of passive verbs are considered as oblique arguments.
% Il est convaincu qu'il doit venir.
  rule comp-obl_ccomp-pass {
    pattern {
      e:H -[1=ccomp]-> D;
      H1 -[aux@pass|comp:aux@pass]-> H}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl}
  }

  rule comp-obl_iobj {
    pattern { e:H -[1=iobj]-> D; }
    commands {e.1=comp;e.2=obl}
  }

% Clausal arguments with control of their subject and marked with an adposition.
% Il songe à venir.
  rule comp-obl_xcomp {
    pattern { e:H -[1=xcomp]-> D; D[upos=VERB]; D -[mark]-> *}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl;e.deep=x}
  }

% Clausal arguments with a copula and marked with an adposition; the copula subject is controlled   by the main verb.
% Ex: Il songe à être soldat.
  rule comp-obl_xcomp-cop {
    pattern { e:H -[1=xcomp]-> D; D -[cop]-> D1; D1[upos=VERB]; D -[mark]-> *}
    commands {e.1=comp;e.2=obl;e.deep=x}
  }
% Direct clausal arguments with control of their subject in presence of a direct object are considered as oblique arguments.
  rule comp-obl_xcomp-obj {
    pattern {
      e:H -[1=xcomp]-> D;D[upos=VERB];
       H -[obj|comp:obj]-> D1}
    without{D -[mark]-> *}
    without{D[VerbFom=Part]}% Participles are considered as predicative arguments
    commands {e.1=comp;e.2=obl;e.deep=x}
  }

% Direct clausal arguments of passive verbs with control of their subject are considered as oblique arguments.
  rule comp-obl_xcomp-pass {
    pattern {
      e:H -[1=xcomp]-> D;D[upos=AUX|VERB,VerbForm=Inf];
       AUX -[aux@pass|comp:aux@pass]-> H}
    without{D -[mark]-> *}
    commands {e.1=comp;e.2=obl;e.deep=x}
  }

% comp:pred
%==========
% A direct argument, which is not a verb, is considered  as a predicative complement in SUD.
% Ex: Il semble malade. - Je le considère comme un ami -
  rule comp-pred_xcomp {
    pattern { e:H -[1=xcomp]-> D}
    without{D[upos=VERB] }
    without{D -[cop]-> *}
    commands {e.1=comp;e.2=pred}
  }

% A participle argument with the control of its subject is considered as predicative argument.
% Ex: Il paraît accablé par la mort de sa femme.
  rule comp_pred_part {
    pattern { e:H -[1=xcomp]-> D; D[upos=VERB,VerbForm=Part]}
    commands {add_edge H -[comp:pred@x]-> D; del_edge e}
  }


% mod
%====
% This rule is too strong for nummod, because in many languages, the relation is DET when D precedes H.
  rule mod {
    pattern { e:H -[1=advcl|advmod|amod|nummod]-> D; }
    commands {e.1=mod}
  }

% Modifiers of nominals that are not marked with a conjunction or an adposition are considered as MOD dependents in SUD.
% Ex: un samedi soir - un homme accablé par le décès de sa femme
  rule mod_acl-nmod {
    pattern { e:H -[1=acl|nmod]-> D; }
    without{ D -[case|mark]-> *}
    commands {e.1=mod}
  }


% subj
%=====
  rule subj {
    pattern { e:H -[1=nsubj|csubj]-> D}
    %without{ H1 -[comp:pred]-> H; H1 -[subj@expl]-> *}
    commands {e.1=subj}
  }


% udep
%=====
% Modifiers of nominals that are marked with a conjunction or an adposition are considered as UDEP dependents in SUD.
  rule udep {
    pattern { e:H -[1=acl|nmod]-> D; D -[case|mark]-> *}
    commands {e.1=udep}
  }

% Adverbs that are modifiers of determined words and marked with a conjunction or an adposition are considered as UDEP dependents in SUD.
   rule udep_advmod-det {
     pattern { H -[det]-> *; e:H -[advmod]-> D; D -[case|mark]-> *}
     commands {del_edge e; add_edge H -[udep]-> D}
   }

% Adverbs that are nominal modifiers and marked with a conjunction or an adposition are considered as UDEP dependents in SUD.
   rule udep_advmod-noun {
     pattern { H[upos=NOUN|NUM|PRON|PROPN|X]; e:H -[advmod]-> D; D -[case|mark]-> *}
     commands {del_edge e; add_edge H -[udep]-> D}
   }

   rule udep_obl {
     pattern { e:H -[1=obl]-> D}
     without{H -[deep=agent]-> D}
     commands {e.1=udep}
   }


% unk
%=====
  rule unk {
    pattern { e:H -[1=dep|fixed]-> D; }
    commands {e.1=unk}
  }

  rule unk_expl {
    pattern { e:H -[expl]-> D; }
    commands {del_edge e;add_edge H -[unk@expl]-> D;}
  }

}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% All aux, case, cop and mark relations are reversed.
% The reverse procedure consists of 4 step
% 1) package [normalize_caus_pass]: change aux:caus into caus relations and aux:pass into pass relations (doing this we have all relations to be reversed at the same top level);
% 2) package [reverse_edges]: reverse edges, add the edge feature deep=REV. shift_in are done and shift_out is done only on the subsets of reversable relations (case, cop, caus, pass, aux, mark); other shift_out operations are postponed to another package;
% 3) package [shift_out_edges]: generic rules for selection of relations that should move from the old head onto the new head;
% 4) rule [remove_rev]: remove the @REV extension;
% 5) package [restore_caus_pass]: symetric to step 1, change caus into aux:caus and pass into aux:pass
package reverse_relations{
  package normalize_caus_pass {
    rule caus {
      pattern { e:M -[aux:caus]-> N }
      commands { del_edge e; add_edge M -[caus]-> N }
    }

    rule pass {
      pattern { e:M -[aux:pass]-> N }
      commands { del_edge e; add_edge M -[pass]-> N }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
% When several dependents compete for replacing the same head, the rule of starting with the closest dependent does not work in the cases of extraposition of the predicate.
% Ex: fr-ud-test_00067-  ... ce que nous avons été ...
% Ex: fr-ud-train_00086- ... quelle que soit la nature de ses pratiques, ...
% The rule that works in all cases is based on an order between types of relations that must be reversed.
%The order chosen below is the following:
% (1) case < aux, caus < aux,  cop < aux, pass < aux;
% (2) case < caus, cop < caus;
% (3) case < cop ;
% (4) aux < mark, case < mark, caus < mark, cop < mark, pass < mark;
% (5) aux <> aux, case <> case, caus <> caus, cop <> cop, mark <> mark, pass <> pass; the order is determined by the order of proximity with respect the head
  package reverse_edges {
    rule aux {
      pattern { e:H -[1=aux, !deep]-> AUX }
      without { H -[1=case|cop|caus|pass, !deep]-> AUX1 } % rule (1) above
      without { H -[1=aux, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=aux, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|mark,!deep]=> AUX;
      }
    }

    rule case {
      pattern { e:H -[1=case, !deep]-> AUX }
      without { H -[1=case, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=case, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|case|caus|cop|mark|pass,!deep]=> AUX;
      }
    }

    rule caus {
      pattern { e:H -[1=caus, !deep]-> AUX }
      without { H -[1=case|cop, !deep]-> AUX1 } % rule (2) above
      without { H -[1=caus, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=caus, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|mark|pass,!deep]=> AUX;
      }
    }

    rule cop {
      pattern { e:H -[1=cop, !deep]-> AUX }
      without { H -[1=case, !deep]-> AUX1 } % rule (3) above
      without { H -[1=cop, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=cop, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|cop|mark|pass,!deep]=> AUX;
      }
    }

    rule mark {
      pattern { e:H -[1=mark, !deep]-> AUX }
      without { H -[1=aux|case|caus|cop|pass, !deep]-> AUX1 } % rule (4) above
      without { H -[1=mark, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=mark, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=mark,!deep]=> AUX;
      }
    }

    rule pass {
      pattern { e:H -[1=pass, !deep]-> AUX }
      without { H -[1=pass, !deep]-> AUX1; AUX << AUX1 ; AUX1 << H } % rule (5) above
      without { H -[1=pass, !deep]-> AUX1; AUX >> AUX1 ; AUX1 >> H } % rule (5) above
      commands {
        e.deep=REV;
        add_edge f: AUX -> H; f.label = e.label;
        del_edge e;
        shift_in H =[^TITLE|IDIOM]=> AUX;
        shift_out H =[1=aux|caus|case|cop|mark|pass,!deep]=> AUX;
      }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
% The source of some relations must rise from the old head D to the new head H.
package shift_out_edges{
% All relations that rise without any restriction.
  rule all{
    pattern {H -[deep=REV]-> D;e: D -[cc|discourse|vocative]-> D1}
    without{ D -[cc]-> D1; H << D1} %Ex:  fr-ud-train_12306 - ... sont soit des fanatiques, soit des hypocrites,
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e;
    }
  }

% Agent relations in causative and impersonal constructions with copula.
  rule agent {
    pattern{H -[1=caus|cop,deep=REV]-> D; e: D -[deep=agent]-> D1}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e;
    }
  }

% All relations that rise inside a clause.
  rule all_mark_embedded{
    pattern {
      H -[1<>mark,deep=REV]-> D;
      e: D -[comp:cleft|dislocated|parataxis|vocative]-> D1;}
    without{H1 -[1 <> cop]-> H; H -[1=case]-> D; D -[parataxis]-> D1}
    %without{H -[1=aux|caus|pass|cop]->D; D -[csubj:impers|nsubj:impers|expl:comp]-> D1}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e;
    }
  }

  rule subj_mark_embedded{
    pattern {
      H -[1<>mark,deep=REV]-> D;
      e: D -[1=subj]-> D1}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e;
    }
  }

% Relations with their source D after the head of a subordinated clause and their dependent D1 before.
  rule all_mark_outside_left{
    pattern {
      H -[1=mark,deep=REV]-> D;
      e: D -[1=dislocated|subj|vocative]-> D1;
      D1 << H; H << D}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e;
    }
  }

% Relations with their source D before the head of a subordinated clause and their dependent D1 afterwards.
  rule all_mark_outside_right{
    pattern {
      H -[1=mark,deep=REV]-> D;
      e: D -[1=dislocated|subj]-> D1;
      D1 >> H; H >> D}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is on the right of D.
  rule H_D_D1{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=mod|punct]-> D1; D << D1}
    without{D -[mod@relcl]-> D1}
    without{H -[1=case]-> D;D -[1=mod]-> D1; D1[upos <> ADP|SCONJ]}
    without{H -[1=mark]-> D;D -[1=mod]-> D1}
    without{H -[1=cop]-> D; D -[mod]-> D1; D1[upos<>ADP|ADV|SCONJ]}
    without{D -> D2; D1 << D2}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = D2.lemma}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = ")"; D2.lemma = "("}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = "»"; D2.lemma = "«"}
    without{D -[punct]-> D2; D2 << D; D -[punct]-> D1; D1.lemma = "]"; D2.lemma = "["}
    without{D1[InTitle=Yes]; H[!InTitle]}% A vérifier
    without{H -[1=case|mark]-> D; H1 -[IDIOM]-> D1; H1 -[HEAD]-> H}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is between D and H.
  rule H_D1_D{
    pattern{
      H -[1=aux|caus|cop|pass,deep=REV]-> D;
      e: D -[mod]-> D1;
      H << D1; D1 << D}
    without{H -[1=cop]-> D; D -[mod]-> D1; D1[upos=ADJ]; D[upos=NOUN]}
    without{ D -> D2; H << D2; D2 << D1}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the left of H and when their dependent D1 is on the right of H.
  rule D_H_D1{
    pattern{H -[deep=REV]-> D;  H >> D; e: D -[1=mod|punct]-> D1; D1 >> H}
    without{D -[comp:obl]-> D1}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the left of D1 and when their dependent D1 is also on the left of H.
  rule D_D1_D{
    pattern{H -[1=cop,deep=REV]-> D; D << D1; e: D -[1=mod]-> D1; D1 << H}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the right of H and when their dependent D1 is on the left of H.
  rule D1_H_D{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=mod|orphan|punct|reparandum|udep]-> D1; D1 << H}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Relations that rise when their source D is on the left of H and when their dependent D1 is on the left of D.
  rule D1_D_H{
    pattern{
      H -[deep=REV]-> D;  H >> D;
      e: D -[1=mod|punct]-> D1; D >> D1
    }
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Conj relations in the general case.
  rule conj{
    pattern{
      H -[deep=REV]-> D; % H << D;
      e: D -[1=conj]-> D1;
      D.upos <> D1.upos;
    }
    without{D[upos=NOUN|PRON|PROPN|NUM|X]; D1[upos=NOUN|PRON|PROPN|NUM|X]}
    without{D[upos=ADJ]; D1[upos=VERB,VerbForm=Part]}
    without{D1[upos=ADJ]; D[upos=VERB,VerbForm=Part]}
    without{D[upos=ADP|ADV]; D1[upos=ADP|ADV]}
    without{D[upos=AUX|VERB]; D1[upos=AUX|VERB]; D1.VerbForm=D.VerbForm}
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

% Conj relations when the conjunct heads are verbs.
  rule conj_verb{
    pattern{
      H -[deep=REV]-> D;  H << D;
      e: D -[1=conj]-> D1;
      D[upos=AUX|VERB,VerbForm]; D1[upos=AUX|VERB, VerbForm];
       D.VerbForm <> D1.VerbForm
    }
    commands{
      add_edge f: H -> D1; f.label = e.label;
      del_edge e
    }
  }

  rule shared{
    pattern{H -[deep=REV]-> D; D[Shared=Yes]}
    commands{H.Shared=Yes; del_feat D.Shared}
  }
}

%%%%%%%%%%%%%%%%%%%%%
% Removing the mark REV for reversed relations.
  rule remove_rev {
    pattern { e: M -[deep=REV]-> N }
    commands{del_feat e.deep}
  }

%%%%%%%%%%%%%%%%%%%%%
% Renaming the reversed relations
  package rename_rel {
    rule case-mark {
      pattern { e:M -[case|mark]-> N }
      commands { del_edge e; add_edge M -[comp:obj]-> N }
    }
    rule caus {
      pattern { e:M -[caus]-> N }
      commands { del_edge e; add_edge M -[comp:aux@caus]-> N }
    }

    rule cop {
      pattern { e:M -[cop]-> N }
      commands { del_edge e; add_edge M -[comp:pred]-> N }
    }

    rule pass {
      pattern { e:M -[pass]-> N }
      commands { del_edge e; add_edge M -[comp:aux@pass]-> N }
    }

    rule other1 {
      pattern { e:M -[aux]-> N }
      commands { add_edge M -[comp:aux]-> N; del_edge e }
    }
    rule other2 {
      pattern { e:M -[1=aux,2=*]-> N }
      commands { add_edge M -[1=comp,2=aux,deep=e.2]-> N; del_edge e }
    }
  }

%%%%%%%%%%%%%%%%%%%%%
  strat main {
    Seq (
      Onf (normalize_caus_pass),
      Onf (reverse_edges),
      Onf (shift_out_edges),
      Onf (remove_rev),
      Onf (rename_rel),
      )
  }
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Move the dependents of a conjunction from the left conjunct to the right conjunct. Dependencies CONJ, DISCOURSE, PARATAXIS and PUNCT are not moved.
package minimize_conj_dep {
% The shared element directly depends on the head of the first conjunct.
  rule minimize_direct {
    pattern {
      C1 -[1=conj]-> C2;
      e:C1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands {
      add_edge f:C2 -> D; f.label = e.label;
      del_edge e;
      D.Shared=Yes
    }
  }


% The shared element depends on the head of the first conjunct via a MOD dependency.
  rule minimize_indirect_mod {
    pattern {
      C1 -[1=conj]-> C2;
      C1 -[mod]-> D1; D1 << C2;
      C2 -[mod]-> D2; D2 << C2; %Ex:fr-ud-dev_01562 - ... plus savoureuse (73%) et plus sucrée (68%) que les autres fraises
      e:D1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands {
      add_edge f:D2 -> D; f.label = e.label;
      del_edge e;
      D.Shared=Yes
    }
  }

% The shared element depends on the head of the first conjunct via a COMP:OBJ dependency.
  rule minimize_indirect_conj-obj {
    pattern {
      C1 -[1=conj]-> C2;
      C1 -[1=comp,2=obj]-> D1; D1 << C2;
      C2 -[1=comp,2=obj]-> D2;
      e:D1 -[^conj|conj:emb|discourse|parataxis|punct]-> D; C2 << D}
    without { C1 -[1=conj]-> C; C2 << C ; C << D} % Ex :fr-ud-train_03266, fr-ud-train_02016
    commands {
      add_edge f:D2 -> D; f.label = e.label;
      del_edge e;
      D.Shared=Yes
    }
  }


}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Mark embedded conj relations with the extension "emb".
rule add_conj_emb{
  pattern {
    N1 -[conj|conj:emb]-> N2;
    e: N2 -[conj]-> N3;
  }
  commands {
    del_edge e; add_edge N2 -[conj:emb]-> N3;
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Dependencies of type CONJ, FIXED and FLAT:NAME grouped into a bouquet are reorganized into a chain.
package chained_relations {
  rule conj {
    pattern {H -[1=conj]-> D1;e: H -[1=conj]-> D2;D1 << D2}
    without {H -[1=conj]-> D;D1 << D; D << D2}
    without {H -[1=conj]-> D;D2 << D}
    commands {
      add_edge f:D1 -> D2; f.label = e.label;
      del_edge e;
    }
  }

  rule fixed {
    pattern {
      H -[unk]-> D1; * -[IDIOM]-> D1;
      e: H -[unk]-> D2;D1 << D2}
    without{H -[unk]-> D; * -[IDIOM]-> D;D1 << D; D << D2}
    without{H -[unk]-> D; * -[IDIOM]-> D;D2 << D;}
    commands{del_edge e;add_edge D1 -[unk]-> D2}
  }

  rule flat {
    pattern {H -[1=flat]-> D1;e: H -[1=flat]-> D2;D1 << D2}
    without {H -[1=flat]-> D;D1 << D; D << D2}
    without {H -[1=flat]-> D;D2 << D}
    commands {add_edge f: D1 -> D2; f.label=e.label; del_edge e}
  }

}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% All remaining UD relations are renamed UNK in SUD
rule unk_rel{
  pattern{e:H -[1<>appos|cc|clf|comp|compound|conj|det|discourse|dislocated|flat|goeswith|list|mod|orphan|parataxis|punct|reparandum|root|subj|udep|unk|vocative|CONV|HEAD|IDIOM|TITLE]-> D}
  commands{del_edge e; add_edge H -[unk]-> D}
}



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat main {
  Seq (
    Onf (eud_to_ud),
    Onf(idioms),
    specific_expr_init, % Initialization of a specific treatment for MWE, titles and translated words.
    Onf (sub_relations), % Package SUB_RELATIONS must precede package REL_EXTENSIONS because this package swat extensions on level 2.
    Onf (rel_extensions),
    Onf (relations), % Package RELATIONS must precede REVERSE_RELATIONS.MAIN because the renaming of some relations depends on the cooccurrence with other relations (nsubj, csubj in the impersonal diathesis for instance)
    reverse_relations.main, % Reversing aux, case, cop and mark relations.
    Onf (minimize_conj_dep), % Package MINIMIZE_CONJ_DEP must follow strategy REVERSE_RELATIONS.MAIN because the shared dependents can depend on relation inversions.Moreover, a dependance can shift from a coordination to an ascendant. Ex: fr-ud-dev_00446 -entre 3 et 6 généralement
    Onf (add_conj_emb), % Package ADD_CONJ_EMB must follow REVERSE_RELATIONS.MAIN because it depends on head changes ; Ex: UD_French-GSD fr-ud-dev_00751
    Onf (chained_relations),% Package CHAINED_RELATIONS must follow package SHIFT_RULES because it can entail a wrong coordination between two elements because of errors in head changes (UD_French-GS: fr-ud-dev_00131).
    specific_expr_close, % Closing a specific treatment for MWE, titles and translated words.
    Onf (unk_rel),
  )
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
strat main0 {
  Seq (
    Onf (eud_to_ud),
    specific_expr_init, % Initialization of a specific treatment for MWE, titles and translated words.
  )
}
