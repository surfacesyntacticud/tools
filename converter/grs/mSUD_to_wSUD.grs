strat main {
  Seq (
    Onf (init),
    Onf (p),
    Onf (final_word),
    Onf (empty_lemma),
  )
}



package init {
  rule pref {
    pattern { X[form=re".+-"]; }
    commands { X.form = X.form[:-1] }
  }
  rule suff {
    pattern { X[form=re"-.+"];}
    commands { X.form = X.form[1:] }
  }

  % replace no_lemma by empty lemma for uniform rules later
  % no_lemma is restored in final package
  rule empty_lemma {
    pattern { X [form<>"__0__", !lemma]}
    commands { X.lemma = "" }
  }

  rule init_mseg {
    pattern { X[form <> "__0__", !MSeg ] }
    commands { X.MSeg = X.form }
  }
  rule init_gloss {
    pattern { X[!Gloss, GE ] }
    commands { X.Gloss = X.GE; del_feat X.GE; }
  }
  rule init_mgloss {
    pattern { X[Gloss, !MGloss ] }
    commands { X.MGloss = X.Gloss }
  }

  rule AlignBegin {
    pattern { X1 [upos, AlignEnd]; X2 [upos, !AlignBegin]; X1 < X2 }
    commands { X2.AlignBegin = X1.AlignEnd }
  }
  rule AlignEnd {
    pattern { X1 [upos, !AlignEnd]; X2 [upos, AlignBegin]; X1 < X2 }
    commands { X1.AlignEnd = X2.AlignBegin }
  }
}

package p {
  rule fwd_Root {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X1 -> X2;
      X2[TokenType=Root]
    }
    commands {
      X1.form = X1.form + X2.form;                      % concatenate the surface form
      X1.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X1.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      append_feats "_" X2 =[re"Translit\|Tone"]=> X1;   % concatenate the surface Translit and Tone
      append_feats X2 =[re"MGloss\|MSeg"]=> X1;
      shift X2 ==> X1;
      X1.upos = X2.upos; X1.TokenType=Root; X1.Gloss=X2.Gloss;
      X1.AlignEnd = X2.AlignEnd;
      del_node X2;
    }
  }
  rule fwd_DerAff {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X1 -> X2;
      X2[TokenType=DerAff, DepPos]
    }
    commands {
      X1.form = X1.form + X2.form;                      % concatenate the surface form
      X1.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X1.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      append_feats "_" X2 =[re"Translit\|Tone"]=> X1;   % concatenate the surface Translit and Tone
      append_feats X2 =[re"MGloss\|MSeg"]=> X1;
      shift X2 ==> X1;
      X1.upos = X2.DerPos; X1.Gloss=X2.Gloss;
      X1.AlignEnd = X2.AlignEnd;
      del_node X2;
    }
  }
  rule fwd_else {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X1 -> X2;
    }
    without { X2[TokenType=Root|DerAff] }
    commands {
      X1.form = X1.form + X2.form;                      % concatenate the surface form
      X1.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X1.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      append_feats "_" X2 =[re"Translit\|Tone"]=> X1;   % concatenate the surface Translit and Tone
      append_feats X2 =[re"MGloss\|MSeg"]=> X1;
      shift X2 ==> X1;
      X1.AlignEnd = X2.AlignEnd;
      del_node X2;
    }
  }

  rule back_Root {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X2 -> X1;
      X1[TokenType=Root]
    }
    commands {
      X2.form = X1.form + X2.form;                      % concatenate the surface form
      X2.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X2.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      prepend_feats "_" X1 =[re"Translit\|Tone"]=> X2;  % concatenate the surface Translit and Tone
      prepend_feats X1 =[re"MGloss\|MSeg"]=> X2;
      shift X1 ==> X2;
      X2.upos = X1.upos; X2.TokenType=Root; X2.Gloss=X1.Gloss;
      X2.AlignBegin = X1.AlignBegin;
      del_node X1;
    }
  }
  rule back_DerAff {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X2 -> X1;
      X1[TokenType=RerAff, DerPos]
    }
    commands {
      X2.form = X1.form + X2.form;                      % concatenate the surface form
      X2.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X2.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      prepend_feats "_" X1 =[re"Translit\|Tone"]=> X2;  % concatenate the surface Translit and Tone
      prepend_feats X1 =[re"MGloss\|MSeg"]=> X2;
      shift X1 ==> X2;
      X2.upos = X1.DerPos; X2.Gloss=X1.Gloss;
      X2.AlignBegin = X1.AlignBegin;
      del_node X1;
    }
  }

  rule back_else {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord; X2 -> X1;
    }
    without { X1[TokenType=Root|DerAff] }
    commands {
      X2.form = X1.form + X2.form;                      % concatenate the surface form
      X2.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X2.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      prepend_feats "_" X1 =[re"Translit\|Tone"]=> X2;  % concatenate the surface Translit and Tone
      prepend_feats X1 =[re"MGloss\|MSeg"]=> X2;
      shift X1 ==> X2;
      X2.AlignBegin = X1.AlignBegin;
      del_node X1;
    }
  }

  rule pre_X1_fct {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord;
      X2[upos=NOUN|VERB|PROPN|ADJ|ADV|ADP|PRON|DET];
      X1[upos=CCONJ|SCONJ|DET|AUX|X|PRON]; % if two pron --> "keep" the second one
      e: H -> X1; f: X1 -> D;
    }
    commands {
      add_edge g: H -> D; g.label = e.label;
      del_edge f;
    }
  }

  rule X1_fct {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord;
      X2[upos=NOUN|VERB|PROPN|ADJ|ADV|ADP|PRON|DET];
      X1[upos=CCONJ|SCONJ|DET|AUX|X|PRON]; % if two pron --> "keep" the second one
    }
    without { X1 -> * }
    without { X2 -> X1 }
    commands {
      X2.form = X1.form + X2.form;                      % concatenate the surface form
      X2.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X2.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      prepend_feats "_" X1 =[re"Translit\|Tone"]=> X2;  % concatenate the surface Translit and Tone
      prepend_feats X1 =[re"MGloss\|MSeg"]=> X2;
      X2.AlignBegin = X1.AlignBegin;
      del_node X1;
    }
  }

  rule pre_X2_fct {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord;
      X1[upos=NOUN|VERB|PROPN|ADJ|ADV|ADP|PRON];
      X2[upos=CCONJ|SCONJ|DET|AUX|X];
      e: H -> X2; f: X2 -> D;
    }
    commands {
      add_edge g: H -> D; g.label = e.label;
      del_edge f;
    }
  }

  rule X2_fct {
    pattern { 
      X1[nWord]; X2[nWord]; X1 < X2; X1.nWord = X2.nWord;
      X1[upos=NOUN|VERB|PROPN|ADJ|ADV|ADP|PRON];
      X2[upos=CCONJ|SCONJ|DET|AUX|X];
    }
    without { X2 -> * }
    without { X1 -> X2 }
    commands {
      X1.form = X1.form + X2.form;                      % concatenate the surface form
      X1.wordform = X1.wordform + X2.wordform;          % concatenate the surface form
      X1.lemma = X1.lemma + X2.lemma;                   % concatenate the surface lemma
      append_feats "_" X2 =[re"Translit\|Tone"]=> X1;   % concatenate the surface Translit and Tone
      append_feats X2 =[re"MGloss\|MSeg"]=> X1;
      X1.AlignEnd = X2.AlignEnd;
      del_node X2;
    }
  }


}

rule final_word {
  pattern { X [Word] }
  commands { X.form = X.Word; del_feat X.Word }
}

% restore no_lemma
rule empty_lemma {
  pattern { X [lemma= ""]}
  commands { del_feat X.lemma }
}
