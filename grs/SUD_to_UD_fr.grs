%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "specif_expr.grs"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
include "SUD_to_UD.grs"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% After the universal determination of nominals, rectification specific to French.
package FR_nom_init {

% Adjectives or verbs that are attributive of nouns are considered as non nominals.
  rule attr_det{
    pattern{
      N0[cat=NOUN|PRON]; N0 -[mod|udep]-> N;
      N[cat=ADJ|VERB,nom]; N -[det]-> *}
    without{ PRO[PronType=Rel]; N -> PRO; N0 << N} % N is not the head of a relative clause with N0 as the antecedent of the relative pronoun.
    commands{del_feat N.nom}
  }

% Numbers that follow a measure unity
  rule unity_num(lex from "lexicons/measure_unit.lp"){
    pattern{N[cat=NUM,nom]; H[lemma=lex.unity]; H -[mod]-> N }
    commands{del_feat N.nom}
  }

% Simple quantity adverbs with a complement introduced with the preposition "de" are considered as nominals.
  rule adv_de_nom(lex from "lexicons/quant_adv.lp"){
    pattern{
     A[cat=ADV,lemma=lex.adv,!nom];
     A -[comp:obl]-> P; P[cat=ADP,lemma=de];
     N[cat=NOUN|NUM|SYM]; P -[comp]-> N
   }
   without{ * -[udep|mod]-> A}
   without{ A[lemma=moins|plus]; P0 -[comp]-> A; P0[lemma=en]}
   commands{ A.nom=y}
 }

% Complex quantity adverbs with a complement introduced with the preposition "de" are considered as nominals.
  rule complex_adv_de_nom{
    pattern{
     A[cat=ADV,!nom];
     A -[comp:obl]-> P; P[cat=ADP,lemma=de];
     N[cat=NOUN|NUM|SYM]; P -[comp]-> N;
     A -[expr]-> H;
     H[_MISC_MWEPOS=ADV, lemma=lex.head];
     H -> D; D[lemma=lex.dep];
   }
   commands{ A.nom=y}
 }
#BEGIN lex
head	dep
%-------------
un	peu
#END
}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of subject relations specific to French.
package FR_subj {
% Subjects of passive participles without auxiliary.
  rule nsubj_pass (lex from "lexicons/passivable_verb.lp") {
    pattern {e:V -[nsubj]-> S; V [VerbForm=Part,Tense=Past,lemma=lex.verb] }
    without {V -[aux|aux:pass]-> *}
    commands { del_edge e; add_edge V -[nsubj:pass]-> S }
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of argument relations specific to French
package FR_comp {
% Universal transformation entails the re-labelling of COMP:OBL relations without adpositions as IOBJ relations, but in French only clitic complements are labelled IOBJ.
  rule oblarg {
    pattern {e:P -[iobj]-> C; C[cat=NOUN|NUM|PROPN] }
    commands { del_edge e; add_edge P -[obl:arg]-> C }
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of MOD relations specific to French
package FR_mod {

% Adverbs antecedent of a relative clause
  rule adv_relcl{
    pattern{
      A[cat=ADV,lemma="là"|partout];
      B[cat=VERB, VerbForm=Fin];
      e:A -[mod]-> B}
    commands{del_edge e; add_edge A -[acl:relcl]-> B}
  }

% Adjectival, verbal or nominal expressions that are hightlighted at the beginning of a clause are considered as incomplete clauses.
% Ex: fr-ud-dev_00407 - Spécialiste des six jours, il a remporté 38 compétitions entre 1931 et 1942.
  rule left_extraposition(lex from "lexicons/nouns_adv.lp") {
    pattern{
      APP[cat=ADJ|NOUN|PRON|PROPN|VERB];
      V -[nsubj|nsubj:pass|nsubj:caus]-> SUBJ; APP << SUBJ; SUBJ << V;
      e: V -[mod]-> APP}
    without{ APP -[case]-> *}
    without{APP[cat=NOUN]; APP -[det|nummod]-> *}
    without{APP[PronType=Rel|Int]}
    without{APP[cat=NOUN,lemma= lex.noun]}
    without{% Modifiers in the form NOUN + PREP + NOUN.
            % Ex: fr-ud-dev_00322 - Couche par couche, Morin arrange ...
      APP[cat=NOUN];
      P[cat=ADP,lemma="après"|par|sur];
      N[cat=NOUN];
      N -[case]-> P;
      APP -[udep]-> N;
      N.lemma=APP.lemma
    }
    commands{ del_edge e; add_edge V -[advcl]-> APP}
  }

% Nouns behaving as circumstancial complements as heads of modifiers in the form of adverbial clauses.
  rule conj_adv-noun(lex from "lexicons/nouns_adv.lp") {
    pattern{
      N[cat=NOUN,lemma=lex.noun];
      e: N -[case]-> C; C[cat=SCONJ];
      * -[mod]-> N
    }
    commands{del_edge e; add_edge N -[mark]-> C}
  }

% Finite clauses, which are not relative clauses and which are not introduced with a conjunction, modifying a nominal expression.
  rule noun_mod_fin-clause{
    pattern{
      N[nom=y];
      V[cat=VERB, VerbForm=Fin];
      e: N -[mod]->V}
    without{PRO[PronType=Rel]; V -> PRO; N << PRO}
    without{PRO[PronType=Rel]; V -> X; X -> PRO;  N << PRO}
    without{PRO[PronType=Rel]; V -> X; X -> Y; Y -> PRO;  N << PRO}
    without{ADV[cat=ADV,lemma="où"|pourquoi]; V -> ADV; N << ADV}
    without{ADV[cat=ADV,lemma="où"|pourquoi]; V -> X; X -> ADV;  N << ADV}
    without{ADV[cat=ADV,lemma="où"|pourquoi]; V -> X; X -> Y; Y -> ADV;  N << ADV}
    commands{ del_edge e; add_edge N -[acl]-> V}
  }

% Numbers that follow a measure unity
  rule unity_num(lex from "lexicons/measure_unit.lp"){
    pattern{D[cat=NUM]; H[lemma=lex.unity]; e:H -[mod]-> D }
    commands{del_edge e; add_edge H -[nummod]-> D}
  }

}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Re-labelling of UDEP relations specific to French
% Expressions in the form "rien de" ou "quelque chose de"
package FR_udep {
  rule amod_de{
    pattern{
      H[cat=NOUN|PRON,lemma=chose|rien]; D[cat=ADJ]; e: H -[udep]-> D;
      D -[case]-> DE; DE[cat=ADP,lemma=de]
    }
    commands{ del_edge e; add_edge H -[amod]-> D}
  }

% D is a numeral modifier of a nominal H that precedes it. In this case, it does not express a modifier of H in the form of a quantity.
  rule nmod_num_after {
    pattern { e:H -[udep]-> D; H[nom=y]; D[cat=NUM]; H << D }
    without{H[lemma=heure]}
    without{D -> PRO; H << PRO; PRO[PronType=Rel]}
    without{D -[mark]-> *}
    commands { del_edge e; add_edge H -[nmod]-> D }
  }

% D is a modifier of a nominal H and it is separated from H with a preposition P. In this case, it does not express a quantity on H.
% Ex: fr-ud-test_00133 - ... Ben Spies et Jorge Lorenzo, tous deux sur Yamaha M1 ...
  rule nmod_num_prep{
    pattern {
      e:H -[udep]-> D; H[nom=y]; D[cat=NUM];
      H -[case]-> P; D << P; P << H }
    commands { del_edge e; add_edge H -[nmod]-> D }
  }

}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

strat FR_main {
  Seq (
    Onf (nom_init),
    Onf (FR_nom_init),
    Onf (unchained_relations),
    Onf (reverse_case_mark),
    Onf (reverse_aux),
    Onf (subj),
    Onf(FR_subj),
    Onf (det),
    Onf (comp),
    Onf(FR_comp),
    Onf (FR_mod),
    Onf (mod),
    Onf (FR_udep),
    Onf (udep),
    Onf (reverse_cop),
    Onf (final),
  )
}

strat FR_main_specif_expr{
  Seq(
    Onf(specif_expr_init),
    FR_main,
    Onf(specif_expr_close)
  )
}


