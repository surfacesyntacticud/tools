package mwepos_put {
  rule fix2 (feature $l0, $l1, $mwepos; file "lexicons/mwe2.lex") {
    pattern {
      H[!mwepos, lemma=$l0];
      H -[fixed]-> F1; F1[lemma=$l1];
    }
    without { H -[fixed]-> F }
    commands { H.mwepos = $mwepos }
  }


  rule fix3 (feature $l0, $l1, $l2, $mwepos; file "lexicons/mwe3.lex") {
    pattern {
      H[!mwepos, lemma=$l0];
      H -[fixed]-> F1; F1[lemma=$l1];
      H -[fixed]-> F2; F2[lemma=$l2];
    }
    without { H -[fixed]-> F }
    commands { H.mwepos = $mwepos }
  }

  rule fix4 (feature $l0, $l1, $l2, $l3, $mwepos; file "lexicons/mwe4.lex") {
    pattern {
      H[!mwepos, lemma=$l0];
      H -[fixed]-> F1; F1[lemma=$l1];
      H -[fixed]-> F2; F2[lemma=$l2];
      H -[fixed]-> F3; F3[lemma=$l3];
    }
    without { H -[fixed]-> F }
    commands { H.mwepos = $mwepos }
  }

  rule fix5 (feature $l0, $l1, $l2, $l3, $l4, $mwepos) {
    pattern {
      H[!mwepos, lemma=$l0];
      H -[fixed]-> F1; F1[lemma=$l1];
      H -[fixed]-> F2; F2[lemma=$l2];
      H -[fixed]-> F3; F3[lemma=$l3];
      H -[fixed]-> F4; F4[lemma=$l4];
    }
    without { H -[fixed]-> F }
    commands { H.mwepos = $mwepos }
  }
#BEGIN
à#le#jour#le#jour#ADV
#END
}

package fixed_del{
  rule prep_noun(feature $prep, $noun){
    pattern {
      PREP[cat=PREP,phon=$prep];
      N[cat=NOUN,phon=$noun];
      fixed_rel: PREP -[fixed]-> N
    }
    commands {
      del_edge fixed_rel; shift PREP ==> N;
      add_edge N -[case]-> PREP
    }
  }
#BEGIN
en#général
en#masse
en#moyenne
en#particulier
en#principe
en#priorité
en#revanche
en#référence
en#somme
en#trombe
en#vrai
#END

  rule tout_en{
    pattern {
      ADV[cat=ADV,phon=tout];
      PREP[cat=PREP,phon=en];
      fixed_rel: ADV -[fixed]-> PREP;
      mark_rel: V -[mark]-> ADV
    }
    commands {
      del_edge fixed_rel;del_edge mark_rel;
      add_edge V -[mark]-> PREP;
      add_edge V -[advmod]-> ADV
    }
  }

  rule prep_que{
    pattern {
      PREP[cat=ADV,phon="après"|avant|depuis|"dès"|pendant|pour|sans];
      QUE[cat=SCONJ,phon=que];
      fixed_rel: PREP -[fixed]-> QUE;
      mark_rel: V -[mark]-> PREP
      }
    commands {
      del_edge fixed_rel;del_edge mark_rel;
      add_edge V -[mark]-> PREP;
      add_edge V -[mark]-> QUE
    }
  }

}
strat main { Seq(Onf (mwepos_put), Onf (fixed_del))}
